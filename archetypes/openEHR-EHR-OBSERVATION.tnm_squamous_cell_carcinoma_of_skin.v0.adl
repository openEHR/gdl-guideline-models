archetype (adl_version=1.4; uid=beefbc0e-9262-4a2e-85be-99b8699a2bb3)
	openEHR-EHR-OBSERVATION.tnm_squamous_cell_carcinoma_of_skin.v0

concept
	[at0000]

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["date"] = <"2026-01-05">
		["name"] = <"Umiah Gohar">
		["organisation"] = <"Cambio CDS">
		["email"] = <"umiah.gohar@cambio.se">
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Katja Mariko Wilde", ...>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the results for each component parameter required to determine the clinical T, N, and M categories for cutaneous squamous cell carcinoma of the skin.">
			keywords = <"cutaneous","squamous","cell","carcinoma","cscc","skin","tnm","clinical","staging">
			copyright = <"© Cambio CDS">
			use = <"Use to record the results for each component parameter required to determine the clinical T, N, and M categories for cutaneous squamous cell carcinoma of the skin.
The archetype supports clinical TNM classification for the following anatomical site groups, each with distinct TNM definitions:
- skin cancer excluding head and neck, eyelid, perianal region, vulva, and penis;
- head and neck skin cancer (excluding eyelid);
- skin cancer of the eyelid.">
			misuse = <"Not to be used for staging other types of malignancies.">
		>
	>
	other_details = <
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Regionala cancercentrum i samverkan. Nationellt vårdprogram skivepitelcancer i huden: Kapitel 8 Kategorisering av tumören. Kunskapsbanken, 2021. Available at: https://kunskapsbanken.cancercentrum.se/diagnoser/skivepitelcancer-i-huden/vardprogram/kategorisering-av-tumoren/
Brierley JD, Gospodarowicz MK, Wittekind C, editors. TNM classification of malignant tumours. 8th ed. Hoboken (NJ): Wiley Blackwell; 2017">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"2b8384e3ce3854e2f62317452188bbf1">
		["build_uid"] = <"9d19fd6e-511a-306c-9d84-3b94adf850e9">
	>

definition
	OBSERVATION[at0000] matches {    -- Clinical TNM classification for cutaneous squamous cell carcinoma of the skin
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									CLUSTER[at0035] occurrences matches {0..1} matches {    -- TNM categories for head and neck skin
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0038] occurrences matches {0..1} matches {    -- T - Primary tumour
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0041,    -- TX - Primary tumour cannot be identified
															at0042,    -- T0 - No evidence of primary tumour
															at0043,    -- Tis - Carcinoma in situ
															at0044,    -- T1 - Tumour diameter ≤ 2.0 cm
															at0045,    -- T2 - Tumour diameter > 2 cm and ≤ 4 cm
															at0046,    -- T3 - Tumour > 4 cm or minor bone erosion or perineural invasion or deep invasion
															at0047,    -- T4a - Tumour with gross cortical bone/marrow invasion
															at0048]    -- T4b - Tumour with skull base or axial skeleton invasion including foraminal involvement and/or vertebral foramen involvement to the epidural space
														}
													}
												}
											}
											ELEMENT[at0039] occurrences matches {0..1} matches {    -- N - Lymph nodes
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0049,    -- N0 - No regional lymph node metastasis
															at0050,    -- N1 - Metastasis in single ipsilateral node ≤ 3 cm, no extranodal extension
															at0051,    -- N2a - Metastasis in single ipsilateral node > 3 cm and ≤ 6 cm, no extranodal extension
															at0052,    -- N2b - Metastasis in multiple ipsilateral nodes ≤ 6 cm, no extranodal extension
															at0053,    -- N2c - Metastasis in bilateral or contralateral nodes ≤ 6 cm, no extranodal extension
															at0054,    -- N3a - Metastasis in node > 6 cm, no extranodal extension
															at0055]    -- N3b - Metastasis in any node(s) with extranodal extension
														}
													}
												}
											}
											ELEMENT[at0040] occurrences matches {0..1} matches {    -- M - Distant metastasis
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0056,    -- M0 - No distant metastasis
															at0057]    -- M1 - Distant metastasis
														}
													}
												}
											}
										}
									}
									CLUSTER[at0037] occurrences matches {0..1} matches {    -- TNM categories for the skin of the eyelids
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0058] occurrences matches {0..1} matches {    -- T - Primary tumour
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0061,    -- T0 - No evidence of primary tumour
															at0062,    -- Tis - Carcinoma in situ
															at0063,    -- T1a - Tumour diameter ≤ 10 mm, no invasion of tarsal plate or eyelid margin
															at0064,    -- T1b - Tumour diameter ≤ 10 mm, invasion of tarsal plate or eyelid margin
															at0065,    -- T1c - Tumour diameter ≤ 10 mm, full-thickness eyelid involvement
															at0077,    -- T2a - Tumour > 10 mm and ≤ 20 mm, no invasion of tarsal plate or eyelid margin
															at0078,    -- T2b - Tumour > 10 mm and ≤ 20 mm, invasion of tarsal plate or eyelid margin
															at0079,    -- T2c - Tumour > 10 mm and ≤ 20 mm, full-thickness eyelid involvement
															at0080,    -- T3a - Tumour > 20 mm and ≤ 30 mm, no invasion of tarsal plate or eyelid margin
															at0081,    -- T3b - Tumour > 20 mm and ≤ 30 mm, invasion of tarsal plate or eyelid margin
															at0082,    -- T3c - Tumour > 20 mm and ≤ 30 mm, full-thickness eyelid involvement
															at0083,    -- T4a - Tumour invading ocular or intraorbital structures
															at0084]    -- T4b - Tumour invading orbital bone, paranasal sinus, lacrimal sac/duct, or brain
														}
													}
												}
											}
											ELEMENT[at0059] occurrences matches {0..1} matches {    -- N - Lymph nodes
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0073,    -- NX - Regional lymph nodes cannot be assessed
															at0074,    -- N0 - No evidence of lymph node involvement
															at0075,    -- N1 - Metastasis in a single ipsilateral node ≤ 3 cm
															at0076]    -- N2 - Metastasis in a single ipsilateral node > 3 cm or bilateral/contralateral nodal metastases
														}
													}
												}
											}
											ELEMENT[at0060] occurrences matches {0..1} matches {    -- M - Distant metastasis
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0071,    -- M0 - No distant metastasis
															at0072]    -- M1 - Distant metastasis
														}
													}
												}
											}
										}
									}
									CLUSTER[at0031] occurrences matches {0..1} matches {    -- TNM categories for non-head and neck skin
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0007] occurrences matches {0..1} matches {    -- T - Primary tumor
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0008,    -- Tx - Primary tumor cannot be identified
															at0009,    -- T0 - No evidence of primary tumour
															at0010,    -- Tis - Carcinoma in situ
															at0011,    -- T1 - Tumour diameter ≤ 2.0 cm
															at0012,    -- T2 - Tumour diameter > 2 cm and ≤ 4 cm
															at0013,    -- T3 - Tumour > 4 cm or minor bone erosion or perineural invasion or deep invasion
															at0014,    -- T4a - Tumour with gross cortical bone/marrow invasion
															at0015]    -- T4b - Tumour with axial skeleton invasion including foraminal involvement and/or vertebral foramen involvement to the epidural space
														}
													}
												}
											}
											ELEMENT[at0016] occurrences matches {0..1} matches {    -- N - Lymph nodes
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0017,    -- NX - Regional lymph nodes cannot be assessed
															at0018,    -- N0 - No regional lymph node metastasis
															at0019,    -- N1 - Single lymph node ≤ 3 cm
															at0020,    -- N2 - Node > 3 cm and ≤ 6 cm or multiple ipsilateral nodes ≤ 6 cm
															at0021]    -- N3 - Node > 6 cm
														}
													}
												}
											}
											ELEMENT[at0022] occurrences matches {0..1} matches {    -- M - Distant metastasis
												value matches {
													DV_CODED_TEXT matches {
														defining_code matches {
															[local::
															at0023,    -- M0 - No distant metastasis
															at0024]    -- M1 - Distant metastatic disease
														}
													}
												}
											}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0004] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0029] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Clinical TNM classification for cutaneous squamous cell carcinoma of the skin">
					description = <"A structured clinical classification used to record site-specific Tumour-Node-Metastasis (TNM) categories for cutaneous squamous cell carcinoma (cSCC) of the skin according to the UICC 8th edition.
The archetype supports clinical TNM classification for different anatomical site groups with distinct staging definitions.">
					comment = <"Clinical TNM classification according to UICC 8th edition (2017)">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0007"] = <
					text = <"T - Primary tumor">
					description = <"Site group: Non-head and neck skin (excluding head & neck, eyelid, perianal, vulva, penis)">
				>
				["at0008"] = <
					text = <"Tx - Primary tumor cannot be identified">
					description = <"">
				>
				["at0009"] = <
					text = <"T0 - No evidence of primary tumour">
					description = <"">
				>
				["at0010"] = <
					text = <"Tis - Carcinoma in situ">
					description = <"">
				>
				["at0011"] = <
					text = <"T1 - Tumour diameter ≤ 2.0 cm">
					description = <"">
				>
				["at0012"] = <
					text = <"T2 - Tumour diameter > 2 cm and ≤ 4 cm">
					description = <"">
				>
				["at0013"] = <
					text = <"T3 - Tumour > 4 cm or minor bone erosion or perineural invasion or deep invasion">
					description = <"">
				>
				["at0014"] = <
					text = <"T4a - Tumour with gross cortical bone/marrow invasion">
					description = <"">
				>
				["at0015"] = <
					text = <"T4b - Tumour with axial skeleton invasion including foraminal involvement and/or vertebral foramen involvement to the epidural space">
					description = <"">
				>
				["at0016"] = <
					text = <"N - Lymph nodes">
					description = <"Site group: Skin (excluding head & neck, eyelid, perianal, vulva, penis)">
				>
				["at0017"] = <
					text = <"NX - Regional lymph nodes cannot be assessed">
					description = <"">
				>
				["at0018"] = <
					text = <"N0 - No regional lymph node metastasis">
					description = <"">
				>
				["at0019"] = <
					text = <"N1 - Single lymph node ≤ 3 cm">
					description = <"">
				>
				["at0020"] = <
					text = <"N2 - Node > 3 cm and ≤ 6 cm or multiple ipsilateral nodes ≤ 6 cm">
					description = <"">
				>
				["at0021"] = <
					text = <"N3 - Node > 6 cm">
					description = <"">
				>
				["at0022"] = <
					text = <"M - Distant metastasis">
					description = <"Site group: Skin (excluding head & neck, eyelid, perianal, vulva, penis)">
				>
				["at0023"] = <
					text = <"M0 - No distant metastasis">
					description = <"">
				>
				["at0024"] = <
					text = <"M1 - Distant metastatic disease">
					description = <"">
				>
				["at0029"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
				["at0031"] = <
					text = <"TNM categories for non-head and neck skin">
					description = <"Clinical TNM categories applicable to cutaneous squamous cell carcinoma of the skin excluding head and neck region, eyelid, perianal region, vulva, and penis, according to UICC 8th edition.">
				>
				["at0035"] = <
					text = <"TNM categories for head and neck skin">
					description = <"Clinical TNM categories applicable to cutaneous squamous cell carcinoma of the head and neck region, excluding eyelid tumours, according to UICC 8th edition.">
				>
				["at0037"] = <
					text = <"TNM categories for the skin of the eyelids">
					description = <"Clinical TNM categories applicable to cutaneous squamous cell carcinoma of the eyelid, according to UICC 8th edition.">
				>
				["at0038"] = <
					text = <"T - Primary tumour">
					description = <"Site group: Head and neck skin (excluding eyelid)">
				>
				["at0039"] = <
					text = <"N - Lymph nodes">
					description = <"Site group: Head and neck skin (excluding eyelid)">
				>
				["at0040"] = <
					text = <"M - Distant metastasis">
					description = <"Site group: Head and neck skin (excluding eyelid)">
				>
				["at0041"] = <
					text = <"TX - Primary tumour cannot be identified">
					description = <"">
				>
				["at0042"] = <
					text = <"T0 - No evidence of primary tumour">
					description = <"">
				>
				["at0043"] = <
					text = <"Tis - Carcinoma in situ">
					description = <"">
				>
				["at0044"] = <
					text = <"T1 - Tumour diameter ≤ 2.0 cm">
					description = <"">
				>
				["at0045"] = <
					text = <"T2 - Tumour diameter > 2 cm and ≤ 4 cm">
					description = <"">
				>
				["at0046"] = <
					text = <"T3 - Tumour > 4 cm or minor bone erosion or perineural invasion or deep invasion">
					description = <"">
				>
				["at0047"] = <
					text = <"T4a - Tumour with gross cortical bone/marrow invasion">
					description = <"">
				>
				["at0048"] = <
					text = <"T4b - Tumour with skull base or axial skeleton invasion including foraminal involvement and/or vertebral foramen involvement to the epidural space">
					description = <"">
				>
				["at0049"] = <
					text = <"N0 - No regional lymph node metastasis">
					description = <"">
				>
				["at0050"] = <
					text = <"N1 - Metastasis in single ipsilateral node ≤ 3 cm, no extranodal extension">
					description = <"">
				>
				["at0051"] = <
					text = <"N2a - Metastasis in single ipsilateral node > 3 cm and ≤ 6 cm, no extranodal extension">
					description = <"">
				>
				["at0052"] = <
					text = <"N2b - Metastasis in multiple ipsilateral nodes ≤ 6 cm, no extranodal extension">
					description = <"">
				>
				["at0053"] = <
					text = <"N2c - Metastasis in bilateral or contralateral nodes ≤ 6 cm, no extranodal extension">
					description = <"">
				>
				["at0054"] = <
					text = <"N3a - Metastasis in node > 6 cm, no extranodal extension">
					description = <"">
				>
				["at0055"] = <
					text = <"N3b - Metastasis in any node(s) with extranodal extension">
					description = <"">
				>
				["at0056"] = <
					text = <"M0 - No distant metastasis">
					description = <"">
				>
				["at0057"] = <
					text = <"M1 - Distant metastasis">
					description = <"">
				>
				["at0058"] = <
					text = <"T - Primary tumour">
					description = <"Site group: Eyelid skin">
				>
				["at0059"] = <
					text = <"N - Lymph nodes">
					description = <"Site group: Eyelid skin">
				>
				["at0060"] = <
					text = <"M - Distant metastasis">
					description = <"Site group: Eyelid skin">
				>
				["at0061"] = <
					text = <"T0 - No evidence of primary tumour">
					description = <"">
				>
				["at0062"] = <
					text = <"Tis - Carcinoma in situ">
					description = <"">
				>
				["at0063"] = <
					text = <"T1a - Tumour diameter ≤ 10 mm, no invasion of tarsal plate or eyelid margin">
					description = <"">
				>
				["at0064"] = <
					text = <"T1b - Tumour diameter ≤ 10 mm, invasion of tarsal plate or eyelid margin">
					description = <"">
				>
				["at0065"] = <
					text = <"T1c - Tumour diameter ≤ 10 mm, full-thickness eyelid involvement">
					description = <"">
				>
				["at0071"] = <
					text = <"M0 - No distant metastasis">
					description = <"">
				>
				["at0072"] = <
					text = <"M1 - Distant metastasis">
					description = <"">
				>
				["at0073"] = <
					text = <"NX - Regional lymph nodes cannot be assessed">
					description = <"">
				>
				["at0074"] = <
					text = <"N0 - No evidence of lymph node involvement">
					description = <"">
				>
				["at0075"] = <
					text = <"N1 - Metastasis in a single ipsilateral node ≤ 3 cm">
					description = <"">
				>
				["at0076"] = <
					text = <"N2 - Metastasis in a single ipsilateral node > 3 cm or bilateral/contralateral nodal metastases">
					description = <"">
				>
				["at0077"] = <
					text = <"T2a - Tumour > 10 mm and ≤ 20 mm, no invasion of tarsal plate or eyelid margin">
					description = <"">
				>
				["at0078"] = <
					text = <"T2b - Tumour > 10 mm and ≤ 20 mm, invasion of tarsal plate or eyelid margin">
					description = <"">
				>
				["at0079"] = <
					text = <"T2c - Tumour > 10 mm and ≤ 20 mm, full-thickness eyelid involvement">
					description = <"">
				>
				["at0080"] = <
					text = <"T3a - Tumour > 20 mm and ≤ 30 mm, no invasion of tarsal plate or eyelid margin">
					description = <"">
				>
				["at0081"] = <
					text = <"T3b - Tumour > 20 mm and ≤ 30 mm, invasion of tarsal plate or eyelid margin">
					description = <"">
				>
				["at0082"] = <
					text = <"T3c - Tumour > 20 mm and ≤ 30 mm, full-thickness eyelid involvement">
					description = <"">
				>
				["at0083"] = <
					text = <"T4a - Tumour invading ocular or intraorbital structures">
					description = <"">
				>
				["at0084"] = <
					text = <"T4b - Tumour invading orbital bone, paranasal sinus, lacrimal sac/duct, or brain">
					description = <"">
				>
			>
		>
	>
