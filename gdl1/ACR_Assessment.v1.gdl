(GUIDE) <
	gdl_version = <"0.1">
	id = <"ACR_Assessment.v1">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"albuminuria", "albumin excretion rate", "chronic kidney disease", "CKD", "albumin:creatinine ratio", "ACR", "KDIGO", "CKD classification", "AER">
				misuse = <"">
				purpose = <"This guide categorizes urinary albumin:creatinine ratio (ACR) based on the Kidney Disease: Improving Global Outcomes (KDIGO) Chronic Kidney Disease Work Group.">
				use = <"Use to categorize urinary albumin:creatinine ratio (ACR) into 1 of 3 possible KDIGO-ACR categories.

KDIGO A1 is normal to moderately increased ACR (less than 30 mg/g), 
KDIGO A2 is moderately increased ACR (30 - 300 mg/g), and 
KDIGO A3 is severely increased ACR (more than 300 mg/g).

May be used in concert with glomerular filtration rate (GFR) categories to stage and evaluate the risk of progression of chronic kidney disease (CKD).">
			>
			["sv"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"albuminuri", "albumin:kreatinin ratio", "ACR", "KDIGO", "kronisk njursvikt">
				misuse = <"">
				purpose = <"Att klassificera albumin/kreatinin-kvot (ACR) baserat på Kidney Disease: Improving Global Outcomes (KDIGO) av Chronic Kidney Disease Work Group.">
				use = <"Använd för klassificera albumin/kreatinin-kvot (ACR) i en av tre möjliga KDIGO-kategorier enligt:

KDIGO A1 - indikerar normal till måttligt förhöjd ACR (mindre än 30 mg/g)
KDIGO A2 - indikerar måttligt förhöjd ACR (30-300 mg/g)
KDIGO A3 - indikerar kraftigt förhöjd ACR (mer än 300 mg/g).

Kan med fördel sammanvägas tillsammans med glomerulär filtrationshastighet (GFR) för gradering och utvärdering av progressionsrisk av kronisk njursvikt.">
			>
		>
		lifecycle_state = <"Author draft">
		original_author = <
			["date"] = <"2016-10-13">
			["email"] = <"models@cambiocds.com">
			["name"] = <"Eneimi Allwell-Brown">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_contributors = <"Dennis Forslund", "Jimmy Axelsson">
		other_details = <
			["references"] = <"Eknoyan G, Lameire N, Eckardt KU, Kasiske BL, Wheeler DC, Levin A, Stevens PE, Bilous RW, Lamb EJ, Coresh J, Levey AS. KDIGO 2012 clinical practice guideline for the evaluation and management of chronic kidney disease. Kidney Int. 2013;3:5-14.">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0006"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.lab_test-microalbumin.v1">
				domain = <"CDS">
				elements = <
					["gt0007"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0078.1]">
					>
				>
			>
			["gt0012"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-EVALUATION.acr_assessment_kdigo.v1">
				domain = <"CDS">
				elements = <
					["gt0013"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/items[at0006]">
					>
					["gt0014"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/items[at0002]">
					>
					["gt0015"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/items[at0010]">
					>
				>
			>
		>
		rules = <
			["gt0008"] = (RULE) <
				when = <"$gt0007<30,mg/gm",...>
				then = <"$gt0013=0|local::at0007|< 30|", "$gt0014=local::at0003|A1|", "$gt0015=local::at0011|Normal to mildly increased ACR|">
				priority = <3>
			>
			["gt0010"] = (RULE) <
				when = <"$gt0007<=300,mg/gm", "$gt0007>=30,mg/gm">
				then = <"$gt0013=1|local::at0008|30 - 300|", "$gt0014=local::at0004|A2|", "$gt0015=local::at0012|Moderately increased ACR|">
				priority = <2>
			>
			["gt0011"] = (RULE) <
				when = <"$gt0007>300,mg/gm",...>
				then = <"$gt0013=2|local::at0009|> 300|", "$gt0014=local::at0005|A3|", "$gt0015=local::at0013|Severely increased ACR|">
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"KDIGO-ACR Assessment">
						description = <"Categorizes urinary albumin:creatinine ratio (ACR). The ACR is a surrogate for microalbuminuria and a useful marker of kidney disease. This guide adopts KDIGO guidelines to categorize ACR for ease of staging and evaluating the progression of chronic kidney disease, in conjunction with GFR categories.">
					>
					["gt0007"] = (TERM) <
						text = <"Urine albumin:creatinine ratio">
						description = <"Ratio of albumin and creatinine in this specimen.">
					>
					["gt0008"] = (TERM) <
						text = <"Assign ACR category A1">
						description = <"Contains the expression logic for categorizing albumin:creatinine ratio A1.">
					>
					["gt0010"] = (TERM) <
						text = <"Assign ACR category A2">
						description = <"Contains the expression logic for categorizing albumin:creatinine ratio A2.">
					>
					["gt0011"] = (TERM) <
						text = <"Assign ACR category A3">
						description = <"Contains the expression logic for categorizing albumin:creatinine ratio A3.">
					>
					["gt0013"] = (TERM) <
						text = <"ACR value">
						description = <"Urinary albumin:creatinine ratio value in mg/g.">
					>
					["gt0014"] = (TERM) <
						text = <"KDIGO ACR category">
						description = <"Urinary albumin:creatinine ratio category based on the Kidney Disease: Improving Global Outcomes (KDIGO) Chronic Kidney Disease Work Group.">
					>
					["gt0015"] = (TERM) <
						text = <"KDIGO ACR description">
						description = <"Textual descriptions of KDIGO ACR categories.">
					>
				>
			>
			["sv"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"KDIGO-ACR utvärdering">
						description = <"Klassificering av albumin/kreatinin-kvot (ACR). ACR utgör ett komplement till mikroalbuminuri och är en värdefull markör för njursjukdom. Denna guideline klassificerar ACR i enlighet med KDIGO-riktlinjerna i syfte att utgöra en del av helhetsbedömning vid gradering och prognostisk utvärdering av kronisk njursvikt. ">
					>
					["gt0007"] = (TERM) <
						text = <"U-albumin/kreatinin-kvot">
						description = <"*(en) Ratio of albumin and creatinine in this specimen.">
					>
					["gt0008"] = (TERM) <
						text = <"Sätt till ACR kategori A1">
						description = <"*(en) Contains the expression logic for categorizing albumin:creatinine ratio A1.">
					>
					["gt0010"] = (TERM) <
						text = <"Sätt till ACR kategori A2">
						description = <"*(en) Contains the expression logic for categorizing albumin:creatinine ratio A2.">
					>
					["gt0011"] = (TERM) <
						text = <"Sätt till ACR kategori A3">
						description = <"*(en) Contains the expression logic for categorizing albumin:creatinine ratio A3.">
					>
					["gt0013"] = (TERM) <
						text = <"ACR värde">
						description = <"*(en) Urinary albumin:creatinine ratio value in mg/g.">
					>
					["gt0014"] = (TERM) <
						text = <"KDIGO ACR kategori">
						description = <"*(en) Urinary albumin:creatinine ratio category based on the Kidney Disease: Improving Global Outcomes (KDIGO) Chronic Kidney Disease Work Group.">
					>
					["gt0015"] = (TERM) <
						text = <"KDIGO ACR beskrivning">
						description = <"*(en) Textual descriptions of KDIGO ACR categories.">
					>
				>
			>
		>
	>
>
