(GUIDE) <
	gdl_version = <"0.1">
	id = <"ASSIGN.v1">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"ASSIGN", "SIGN", "cardiovascular risk">
				misuse = <"As it is an estimate of risk and should not be used alone or an absolute indicator of risk without further investigation using adddtional information and tools. Readings of blood pressure and cholesterol must not be input without due attention to the medication used (if any) to reduce blood pressure and/or cholesterol.
Only for individuals in Scotland (unless postcode mapping is also adjusted to the new region - or SIGN is used instead)">
				purpose = <"ASSIGN is a 10 year cardiovascular risk score which was developed to identify and prioritise disease-free individuals who have been assessed to be of high risk of developing cardiovascular disease in a ten-year period. ">
				use = <"The score is based on historical data and is a number between 0 and 99 which represents a percentage at 10-years of developing a cardiovascular disease (including coronary heart disease, cerebrovascular disease (inc. transient ischemic attacks)) in individuals who are currently disease-free.

The ASSIGN score extends on the SIGN score by taking into account the individual's postcode (an approximation of the social economic status of the person) into account - mapped to the SIMD (Scottish Index of Multiple Deprivation 2012) and is thus related to the Scottish population [a similar mapping to other regions can be done in order to use ASSIGN elsewhere.]

The ASSIGN score risk factors are:

older age group
men > women
poorer neighbourhoods
family history (coronary disease or stroke)
diabetes mellitus of target
rheumatoid arthritis of target
tobacco smoking (cigarettes, pipe tobacco, or cigars)
the blood pressure reading
the blood total cholesterol reading
the blood HDL-cholesterol reading

All the risk factors are combined to give a score between 1 and 99. The calculation used is based on a rather long formula involving many different coefficients related to risk. The calculation is based on [4]. The higher the score, the higher the risk of cardiovascular disease.
An ASSIGN score of > 20 is high and identifies individuals of high risk of cardiovascular disease allowing for timely intervention.">
			>
		>
		lifecycle_state = <"Author draft">
		original_author = <
			["date"] = <"2016-11-06">
			["email"] = <"models@cambiocds.com">
			["name"] = <"Syeeda S Farruque">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_contributors = <"Dennis Forslund", "Jimmy Axelsson">
		other_details = <
			["references"] = <"Ref. 1: Tunstall-Pedoe H, Woodward M, Tavendale R, A'Brook R, McCluskey MK. Comparison of the prediction by 27 different factors of coronary heart disease and death in men and women of the Scottish Heart Health Study: cohort study. BMJ 1997; 315: 722-729.

Ref. 2: Tunstall-Pedoe H, Woodward M, for the SIGN group on risk estimation. By neglecting deprivation, cardiovascular risk scoring will exacerbate social gradients in disease. Heart 2006; 92 (3): 307-10. Epub 2005 Sep15.

Ref. 3: Woodward M, Brindle P, Tunstall-Pedoe H, for the SIGN group on risk estimation. Adding social deprivation and family history to cardiovascular risk assessment: the ASSIGN score from the Scottish Heart Health Extended Cohort (SHHEC). Heart 2007: 93(2):172-6.

Ref. 4: Risk calculator excel. Written by Dr Rupert Payne, The University of Edinburgh, May 2010. Further advice on the appropriate use of the risk equations can be found at http://cvrisk.mvm.ed.ac.uk/help.htm">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0006"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.assign_score.v1">
				domain = <"EHR">
				elements = <
					["gt0007"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0010]">
					>
					["gt0008"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0011]">
					>
					["gt0009"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0012]">
					>
					["gt0010"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0013]">
					>
					["gt0011"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0014]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0013"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.assign_score.v1">
				domain = <"CDS">
				elements = <
					["gt0014"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
				>
			>
			["gt0022"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.lab_test-lipids.v1">
				domain = <"EHR">
				elements = <
					["gt0023"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0078.5]">
					>
					["gt0030"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0078.3]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0025"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.basic_demographic.v1">
				domain = <"EHR">
				elements = <
					["gt0026"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
					["gt0027"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0013]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0028"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.blood_pressure.v1">
				domain = <"EHR">
				elements = <
					["gt0029"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0006]/data[at0003]/items[at0004]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
		>
		rules = <
			["gt0021"] = (RULE) <
				when = <"$gt0014==null", "$gt0030.units=='mmol/l'", "$gt0023.units=='mmol/l'", "$gt0011.units=='1'", "$gt0010.units=='1/d'", "$gt0027.units=='a'", "$gt0029.units=='mm[Hg]'", "$gt0026==local::at0006|Female|">
				then = <"$gt0014.units='1'", "$gt0014.magnitude=(100*(1-(0.9365^(2.718281828^((((((((((0.01064*$gt0029.magnitude)+(0.07203*$gt0027.magnitude))+(0.2724*$gt0009.value))+(0.1272*$gt0023.magnitude))-(0.55836*$gt0030.magnitude))+(0.97727*$gt0008.value))+(0.49159*$gt0007.value))+(0.02724*$gt0010.magnitude))+(0.1*(0.09386*$gt0011.magnitude)))-5.418379)))))">
				priority = <1>
			>
			["gt0031"] = (RULE) <
				when = <"$gt0026==local::at0005|Male|", "$gt0010.units=='1/d'", "$gt0011.units=='1'", "$gt0030.units=='mmol/l'", "$gt0023.units=='mmol/l'", "$gt0027.units=='a'", "$gt0029.units=='mm[Hg]'", "$gt0014==null">
				then = <"$gt0014.units='1'", "$gt0014.magnitude=(100*(1-(0.8831^(2.718281828^((((((((((0.01183*$gt0029.magnitude)+(0.05698*$gt0027.magnitude))+(0.2005*$gt0009.value))+(0.22286*$gt0023.magnitude))-(0.53684*$gt0030.magnitude))+(0.81558*$gt0008.value))+(0.275*$gt0007.value))+(0.02005*$gt0010.magnitude))+(0.1*(0.06296*$gt0011.magnitude)))-5.447113)))))">
				priority = <2>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"ASSIGN">
						description = <"The ASSIGN score was developed in 2006 at the University of Dundee, Scotland based on the SIGN (Scottish Intercollegiate Guidelines Network) method of ascertaining cardiovascular risk estimation. The reference range of the SIMD is 0.94 – 89.89 and the mean value is 20.68.">
					>
					["gt0003"] = (TERM) <
					>
					["gt0005"] = (TERM) <
					>
					["gt0007"] = (TERM) <
						text = <"Family history of CHD/Stroke">
						description = <"FH of CHD/stroke increases the risk">
					>
					["gt0008"] = (TERM) <
						text = <"Is the subject diabetic?">
						description = <"Diabetes in the subject increases the risk">
					>
					["gt0009"] = (TERM) <
						text = <"Rheumatoid arthritis?">
						description = <"Rheumatoid arthritis in the subject increases the risk by approximately 10 cigrattes daily">
					>
					["gt0010"] = (TERM) <
						text = <"Cigarettes smoked daily or equivalent">
						description = <"Number of cigarettes smoked daily or equivalent ">
					>
					["gt0011"] = (TERM) <
						text = <"SIMD">
						description = <"Scottish Index of Multiple Deprivation 2012 related to mapping postcodes in Scotland to codes that indicate degrees of deprivation.">
					>
					["gt0012"] = (TERM) <
						text = <"Event time">
						description = <"The exact time of a single timed event during the Observation. Can represent either a specific point-in-time or an interval event.">
					>
					["gt0014"] = (TERM) <
						text = <"ASSIGN score">
						description = <"A score of cardiovascular risk which is a number between 1 and 99 (roughly representing percentage risk). The score is based on many factors">
					>
					["gt0015"] = (TERM) <
						text = <"set Simd">
					>
					["gt0016"] = (TERM) <
						text = <"set RA">
					>
					["gt0017"] = (TERM) <
						text = <"Set diabetes">
					>
					["gt0018"] = (TERM) <
						text = <"Set CHD/stroke">
					>
					["gt0019"] = (TERM) <
						text = <"Set cpd">
					>
					["gt0020"] = (TERM) <
						text = <"Set elements">
					>
					["gt0021"] = (TERM) <
						text = <"Calculate total score for women">
					>
					["gt0023"] = (TERM) <
						text = <"Total Cholesterol">
						description = <"The total cholesterol concentration in the sample.">
					>
					["gt0024"] = (TERM) <
						text = <"Laboratory test result identifier">
						description = <"The identifier given to the laboratory test result of a pathology 
investigation.">
					>
					["gt0026"] = (TERM) <
						text = <"Gender">
						description = <"*">
					>
					["gt0027"] = (TERM) <
						text = <"Age">
						description = <"Age in years, and for babies: months, weeks or days">
					>
					["gt0029"] = (TERM) <
						text = <"Systolic">
						description = <"Peak systemic arterial blood pressure  - measured in systolic or contraction phase of the heart cycle.">
					>
					["gt0030"] = (TERM) <
						text = <"HDL Cholesterol">
						description = <"HDL-Cholesterol level in the sample.">
					>
					["gt0031"] = (TERM) <
						text = <"Calculate total score for men">
					>
				>
			>
		>
	>
>
