(GUIDE) <
	gdl_version = <"0.1">
	id = <"AUDIT-C.assessment.v1">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"AUDIT", "AUDIT-C", "alcohol misuse", "alcohol abuse", "alcohol use disorder", "psychiatry", "primary care", "screening", "DSM">
				misuse = <"Only intended for the AUDIT-C, not the full version.">
				purpose = <"To assess score generated in accordance with AUDIT-C, which is used to screen for alcohol abuse.">
				use = <"Use to assess score generated in accordance with AUDIT-C, which is used to screen for alcohol abuse.

The AUDIT-C contains the first three questions from the full Alcohol Use Disorders Identification Test (AUDIT) which was a World Health Organization-sponsored project. 

- How often do you have a drink containing alcohol?
- How many drinks containing alcohol do you have on a typical day when you are drinking?
- How often do you have six or more drinks on one occasion?

The questions each contribute points to the total score which ranges from 0-12.  The total score is interpreted as follows:

Female - ≥3p indicates alcohol misuse
Male - ≥4p indicates alcohol misuse
A score of 5p or more is consistent with alcohol abuse and possible liver damage
">
			>
			["sv"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"AUDIT", "AUDIT-C", "alkoholmissbruk", "beroende", "missbruk", "riskbruk", "psykiatri", "screening", "DSM", "screening">
				misuse = <"Endast avsedd för registrering av AUDIT-C och ej fullständiga AUDIT.">
				purpose = <"Att utvärdera poäng genererad i enlighet med AUDIT-C, vilken används som screening för alkoholmissbruk.">
				use = <"Använd för att  utvärdera poäng genererad i enlighet med AUDIT-C, vilken används som screening för alkoholmissbruk.

AUDIT-C innehåller de tre första frågorna från det fullständiga formuläret Alcohol Use Disorders Identification Test (AUDIT) vilket är ett WHO-finansierat projekt.

- Hur ofta dricker du alkohol?
- Hur många glas dricker du en typisk dag då du dricker alkohol?
- Hur ofta dricker du (kvinna) 4 eller (man) 5 glas eller mer vid samma tillfälle?

Vardera fråga bidrar med poäng till totalsumman vilken uppgår till maximalt 12p. Resultatet tolkas enligt:

Kvinna - ≥3p indikerar riskbruk av alkohol
Man - ≥4p  indikerar riskbruk av alkohol
5p eller mer är förenligt med alkoholmissbruk och potentiell leverskada.">
			>
		>
		original_author = <
			["date"] = <"2017-02-27">
			["email"] = <"models@cambiocds.com">
			["name"] = <"Dennis Forslund">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_contributors = <"Jimmy Axelsson",...>
		other_details = <
			["references"] = <"Bush K, Kivlahan DR, McDonell MB, Fihn SD, Bradley KA. The AUDIT alcohol consumption questions (AUDIT-C): an effective brief screening test for problem drinking. Ambulatory Care Quality Improvement Project (ACQUIP). Alcohol Use Disorders Identification Test. Arch Intern Med. 1998 Sep 14;158(16):1789-95.

Bradley KA, et. al. AUDIT-C as a brief screen for alcohol misuse in primary care. Alcohol Clin Exp Res. 2007 Jul;31(7):1208-17. Epub 2007 Apr 19.">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0002"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-EVALUATION.audit_c_assessment.v1">
				domain = <"CDS">
				elements = <
					["gt0005"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/items[at0002]">
					>
				>
			>
			["gt0003"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.basic_demographic.v1">
				domain = <"CDS">
				elements = <
					["gt0007"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
				>
			>
			["gt0004"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.audit_c.v1">
				domain = <"CDS">
				elements = <
					["gt0006"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0023]">
					>
				>
			>
		>
		rules = <
			["gt0008"] = (RULE) <
				when = <"$gt0007==local::at0006|Female|", "$gt0006.magnitude<3">
				then = <"$gt0005=0|local::at0003|Consistent with normal consumption|",...>
				priority = <6>
			>
			["gt0009"] = (RULE) <
				when = <"$gt0007==local::at0006|Female|", "($gt0006.magnitude==3)||($gt0006.magnitude==4)">
				then = <"$gt0005=1|local::at0004|Consistent with alcohol misuse|",...>
				priority = <5>
			>
			["gt0010"] = (RULE) <
				when = <"$gt0007==local::at0006|Female|", "$gt0006.magnitude>=5">
				then = <"$gt0005=2|local::at0005|Consistent with alcohol abuse and possible liver damage|",...>
				priority = <4>
			>
			["gt0011"] = (RULE) <
				when = <"$gt0007==local::at0005|Male|", "$gt0006.magnitude<4">
				then = <"$gt0005=0|local::at0003|Consistent with normal consumption|",...>
				priority = <3>
			>
			["gt0012"] = (RULE) <
				when = <"$gt0007==local::at0005|Male|", "$gt0006.magnitude==4">
				then = <"$gt0005=1|local::at0004|Consistent with alcohol misuse|",...>
				priority = <2>
			>
			["gt0013"] = (RULE) <
				when = <"$gt0007==local::at0005|Male|", "$gt0006.magnitude>=5">
				then = <"$gt0005=2|local::at0005|Consistent with alcohol abuse and possible liver damage|",...>
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"AUDIT-C assessment">
						description = <"Assessment of score generated in accordance with AUDIT-C, which is used to screen for alcohol abuse.">
					>
					["gt0005"] = (TERM) <
						text = <"AUDIT-C">
						description = <"*">
					>
					["gt0006"] = (TERM) <
						text = <"Total score">
						description = <"Sum of all factors ">
					>
					["gt0007"] = (TERM) <
						text = <"Gender">
						description = <"The patient's gender.">
					>
					["gt0008"] = (TERM) <
						text = <"AUDIT C female - normal">
					>
					["gt0009"] = (TERM) <
						text = <"AUDIT C female - misuse">
					>
					["gt0010"] = (TERM) <
						text = <"AUDIT C female - abuse">
					>
					["gt0011"] = (TERM) <
						text = <"AUDIT C male - normal">
					>
					["gt0012"] = (TERM) <
						text = <"AUDIT C male - misuse">
					>
					["gt0013"] = (TERM) <
						text = <"AUDIT C male - abuse">
					>
				>
			>
			["sv"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"AUDIT-C assessment">
						description = <"Utvärdering av poäng genererad i enlighet med AUDIT-C, vilken används som screening för alkoholmissbruk.
">
					>
					["gt0005"] = (TERM) <
						text = <"AUDIT-C">
						description = <"*(en) *">
					>
					["gt0006"] = (TERM) <
						text = <"Resultat">
						description = <"*(en) Sum of all factors ">
					>
					["gt0007"] = (TERM) <
						text = <"Kön">
						description = <"*(en) The patient's gender.">
					>
					["gt0008"] = (TERM) <
						text = <"AUDIT C kvinna - normal">
					>
					["gt0009"] = (TERM) <
						text = <"AUDIT C kvinna - riskbruk">
					>
					["gt0010"] = (TERM) <
						text = <"AUDIT C kvinna - missbruk">
					>
					["gt0011"] = (TERM) <
						text = <"AUDIT C man - normal">
					>
					["gt0012"] = (TERM) <
						text = <"AUDIT C man - riskbruk">
					>
					["gt0013"] = (TERM) <
						text = <"AUDIT C man - missbruk">
					>
				>
			>
		>
	>
>
