(GUIDE) <
	gdl_version = <"0.1">
	id = <"Anion_Gap_Delta_Ratio_evaluation.v1">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"anion gap", "delta ratio", "metabolic acidosis">
				misuse = <"Always combine such tools with sound clinical judgment when evaluating the results.">
				purpose = <"This tool evaluates the anion gap, both corrected for albumin and the resultant delta ratio">
				use = <"There are many varying differential diagnoses based on the type of anion gap acidosis states. The delta ratio and corrected anion gap (for serum albumin) helps in this differentiation.

The delta ratio helps to evaluate whether there is a mixed acid-base picture in the presence of an anion gap acidosis. The interpretation is shown below:

Delta Ratio Interpretation:

Delta Ratio... 	Suggests...
< 0.4 	                   Hyperchloremic normal anion gap acidosis
< 1 	                   High AG & normal AG acidosis
1 to 2 	                   Pure Anion Gap Acidosis
                                      Lactic acidosis: average value 1.6
                                      DKA more likely to have a ratio closer to 1 due to urine ketone loss
> 2 	                    High AG acidosis and a concurrent metabolic alkalosis or a pre-existing compensated respiratory acidosis

Also a corrected Anion Gap that is higher than 1 mEq/L (mmol/L) in comparison to a non-corrected anion gap, is indicative of a high anion gap acidosis
">
			>
			["sv"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"anjongap", "delta ratio", "acidos">
				misuse = <"Endast avsedd att utgöra understöd till klinisk bedömning.">
				purpose = <"Att utvärdera anjongap, korrigerat anjongap, och deltaratio vid acidos.">
				use = <"Anjongap och delta ratio kan användas för att differentiera mellan olika tillstånd som orsakar acidos. 

Resultatet tolkas enligt:

Delta Ratio tolkning

Delta Ratio... 	Indikerar...
< 0.4 	                   Hyperkloremisk metabol acidos (normalt AG)
< 1 	                   Förhöjt AG & normal AG acidos
1-2 	                   Acidos pga AG
                                      Laktatacidos: genomsnittsvärde 1.6
                                      Diabetisk ketoacidos har mer sannolikt ratio närmare 1 till följd av ketonutsöndring via urin
> 2 	                    Högt AG och samtidig metabol alkalos eller sedan tidigare föreliggande kompenserad respiratorisk acidos.

AG = anjongap

Tillägg: korrigerat anjongap >1 mEq/L (mmol/L) jämfört med icke korrigerat anjongap indikerar acidos till följd av högt anjongap.
">
			>
		>
		lifecycle_state = <"Author draft">
		original_author = <
			["date"] = <"2017-08-13">
			["email"] = <"models@cambiocds.com">
			["name"] = <"Syeeda S Farruque">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_contributors = <"Dennis Forslund, Cambio Healthcare Systems",...>
		other_details = <
			["references"] = <"Ref. 1: Oh MS, Carroll HJ. The anion gap. N. Engl. J. Med. 1977; 297 (15): 814–7.doi:10.1056/NEJM197710132971507. PMID 895822">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0002"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-EVALUATION.anion_gap_delta_ratio_evaluation.v1">
				domain = <"CDS">
				elements = <
					["gt0011"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/items[at0002]">
					>
					["gt0012"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/items[at0005]">
					>
				>
			>
			["gt0003"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.anion_gap.v1">
				domain = <"CDS">
				elements = <
					["gt0007"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0005]">
					>
					["gt0008"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
					["gt0010"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0008]">
					>
				>
			>
		>
		rules = <
			["gt0016"] = (RULE) <
				when = <"$gt0007>$gt0008",...>
				then = <"$gt0011=1|local::at0004|High anion gap acidosis|",...>
				priority = <6>
			>
			["gt0017"] = (RULE) <
				when = <"$gt0007<=$gt0008",...>
				then = <"$gt0011=0|local::at0003|Non anion gap acidosis|",...>
				priority = <5>
			>
			["gt0018"] = (RULE) <
				when = <"$gt0010<0.4,1",...>
				then = <"$gt0012=0|local::at0006|Hyperchloremic normal anion gap acidosis|",...>
				priority = <4>
			>
			["gt0019"] = (RULE) <
				when = <"$gt0010<1,1", "$gt0010>=0.4,1">
				then = <"$gt0012=1|local::at0007|High AG & normal AG acidosis|",...>
				priority = <3>
			>
			["gt0020"] = (RULE) <
				when = <"$gt0010<=2,1", "$gt0010>=1,1">
				then = <"$gt0012=2|local::at0008|Pure anion gap acidosis/Lactic acidosis (see text for more info)|",...>
				priority = <2>
			>
			["gt0021"] = (RULE) <
				when = <"$gt0010>2,1",...>
				then = <"$gt0012=3|local::at0009|High AG acidosis and metabolic alkalosis (see text for more info)|",...>
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Anion Gap and delta ratio evaluation">
						description = <"This tool evaluates the anion gap, both corrected for albumin and the resultant delta ratio">
					>
					["gt0005"] = (TERM) <
						text = <"Anion gap">
						description = <"Anion Gap = Na - (Cl + HCO3-)">
					>
					["gt0006"] = (TERM) <
						text = <"Delta ratio">
						description = <"Delta ratio = (AG – 12)/(24 - [HCO3-])">
					>
					["gt0007"] = (TERM) <
						text = <"Corrected AG">
						description = <"Corrected Anion Gap = Anion Gap + (2.5 * (4 - Albumin))">
					>
					["gt0008"] = (TERM) <
						text = <"Anion gap">
						description = <"Anion Gap = Na - (Cl + HCO3-)">
					>
					["gt0009"] = (TERM) <
						text = <"Corrected AG">
						description = <"Corrected Anion Gap = Anion Gap + (2.5 * (4 - Albumin))">
					>
					["gt0010"] = (TERM) <
						text = <"Delta ratio">
						description = <"Delta ratio = (AG – 12)/(24 - [HCO3-])">
					>
					["gt0011"] = (TERM) <
						text = <"Anion Gap evaluation">
						description = <"If the corrected Anion Gap more than 1 mEq/L higher than the AG, there is a high anion gap acidosis">
					>
					["gt0012"] = (TERM) <
						text = <"Delta ratio evaluation">
						description = <"The delta ratio helps to evaluate whether there is a mixed acid-base picture in the presence of an anion gap acidosis. ">
					>
					["gt0013"] = (TERM) <
						text = <"set anion gap">
					>
					["gt0014"] = (TERM) <
						text = <"set cAG">
					>
					["gt0015"] = (TERM) <
						text = <"set Delta r">
					>
					["gt0016"] = (TERM) <
						text = <"Set High AG acidosis">
					>
					["gt0017"] = (TERM) <
						text = <"Set no AG acidosis">
					>
					["gt0018"] = (TERM) <
						text = <"Set Delta Ratio < 0.4">
					>
					["gt0019"] = (TERM) <
						text = <"Set Delta Ratio < 1">
					>
					["gt0020"] = (TERM) <
						text = <"Set Delta Ratio 1-2">
					>
					["gt0021"] = (TERM) <
						text = <"Set Delta Ratio > 2">
					>
				>
			>
			["sv"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Anjongap - utvärdering">
						description = <"Utvärdering av värden erhållna för beräkning av anjongap.">
					>
					["gt0005"] = (TERM) <
						text = <"Anjongap">
						description = <"Anion Gap = Na - (Cl + HCO3-)">
					>
					["gt0006"] = (TERM) <
						text = <"Delta ratio">
						description = <"Delta ratio = (AG – 12)/(24 - [HCO3-])">
					>
					["gt0007"] = (TERM) <
						text = <"Korrigerat AG">
						description = <"Corrected Anion Gap = Anion Gap + (2.5 * (4 - Albumin))">
					>
					["gt0008"] = (TERM) <
						text = <"Anjongap">
						description = <"Anion Gap = Na - (Cl + HCO3-)">
					>
					["gt0009"] = (TERM) <
						text = <"Korrigerat AG">
						description = <"Corrected Anion Gap = Anion Gap + (2.5 * (4 - Albumin))">
					>
					["gt0010"] = (TERM) <
						text = <"Delta ratio">
						description = <"Delta ratio = (AG – 12)/(24 - [HCO3-])">
					>
					["gt0011"] = (TERM) <
						text = <"Utvärdering - anjongap">
						description = <"">
					>
					["gt0012"] = (TERM) <
						text = <"Utvärdering - delta ratio">
						description = <"">
					>
					["gt0013"] = (TERM) <
						text = <"CDS anjongap">
					>
					["gt0014"] = (TERM) <
						text = <"CDS korrigerat AG">
					>
					["gt0015"] = (TERM) <
						text = <"CDS delta ratio">
					>
					["gt0016"] = (TERM) <
						text = <"CDS förhöjt AG acidos">
					>
					["gt0017"] = (TERM) <
						text = <"CDS icke AG-relaterad acidos">
					>
					["gt0018"] = (TERM) <
						text = <"CDS Delta Ratio < 0.4">
					>
					["gt0019"] = (TERM) <
						text = <"CDS Delta Ratio < 1">
					>
					["gt0020"] = (TERM) <
						text = <"CDS Delta Ratio 1-2">
					>
					["gt0021"] = (TERM) <
						text = <"CDS Delta Ratio > 2">
					>
				>
			>
		>
	>
>
