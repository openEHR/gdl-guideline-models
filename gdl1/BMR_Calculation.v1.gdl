(GUIDE) <
	gdl_version = <"0.1">
	id = <"BMR_Calculation.v1">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"energy need", "daily calorie need", "thermogenesis", "lean body mass", "resting metabolic rate", "resting daily energy expenditure", "RMR", "RDEE">
				misuse = <"Consider factors that may lower or increase BMR.">
				purpose = <"BMR is used in determining an individual's daily caloric needs as well as in developing and modifying nutritional plans.">
				use = <"To calculate Basal Metabolic Rate [BMR] (also Resting Daily Energy Expenditure, [RDEE] and Resting Metabolic Rate [RMR]) using;
Harris–Benedict equations:
    BMR = 66.4730 + ( 13.7516 x weight in kg ) + ( 5.0033 x height in cm ) - ( 6.7550 x age in years ); for males
    BMR = 655.0955 + ( 9.5634 x weight in kg ) + ( 1.8496 x height in cm ) - ( 4.6756 x age in years ); for females
(Revised) Harris-Benedict equations:
    BMR = 88.362 + ( 13.397 x weight in kg ) + ( 4.799 x height in cm ) - ( 5.677 x age in years ); for males
    BMR = 447.593 + ( 9.247 x weight in kg ) + ( 3.098 x height in cm ) - (4.330 x age in years ); for females
Schofield equations:
    Age (years)	            Equation (kcal/day)			Standard error of estimation (SEE)
 - for males:
    < 3	            59.512 × W - 30.4			70
    3–10	            22.706 × W + 504.3			67
    10–18	            17.686 × W + 658.2			105
    18–30	            15.057 × W + 692.2			153
    30–60	            11.472 × W + 873.1			167
    60	            11.711 × W + 587.7			164
 - for females:
    < 3	            58.317 × W - 31.1			59
    3–10	            20.315 × W + 485.9			70
    10–18	            13.384 × W + 692.6			111
    18–30	            14.818 × W + 486.6			119
    30–60	            8.126 × W + 845.6			111
    >60	            9.082 × W + 658.5			108
 - Schofield standard error of estimation (SEE) represents a confidence interval (+/-) for the Schofield BMR, so that calculated Schofield BMR is adjusted upwards (+ SEE) in leaner muscular individuals and individuals at the younger end of the age range equations; and adjusted downwards (- SEE) in obese individuals and individuals at the older end of the age ranges.
Mifflin St Jeor equations:
    RMR = 9.99 x weight (kg) + 6.25 x height (cm) - 4.92 x age(years) + 5; for males
    RMR = 9.99 x weight (kg) + 6.25 x height (cm) - 4.92 x age (years) - 161; for females
Katch-McArdle/Cunningham equation:
    RDEE = 370 + (21.6 x LBM), where LBM = lean body mass in kg.

BMR may be over or under estimated by up to 20% depending on body fat content, weight training, stress, illness, drugs, food intake, altitude, enviromental temperature and even menopause.">
			>
			["sv"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"energibehov", "dagligt energibehov", "lean body mass", "Harris-Benedict", "Schofield", "Mifflin St Jeor", "Katch-McArdle/Cunningham">
				misuse = <"Beakta faktorer som kan tänkas höja eller sänka basalmetabolismen för adekvat näringstillförsel.">
				purpose = <"Registrera data för basalmetabolism (BMR), som används för att avgöra en individs dagliga energibehov och utveckla samt modifiera planerat näringsintag. ">
				use = <"Registrera data för basalmetabolism (Basal Metabolic Rate – BMR, även Resting Daily Energy Expenditure, [RDEE] och Resting Metabolic Rate [RMR]) med hjälp av följande formler;

Harris–Benedict:
    BMR man = 66.4730 + ( 13.7516 x vikt ) + ( 5.0033 x längd ) - ( 6.7550 x ålder );
    BMR kvinna = 655.0955 + ( 9.5634 x vikt ) + ( 1.8496 x längd ) - ( 4.6756 x ålder );
Vikt anges i kg, längd i cm och ålder i år.

Reviderade Harris-Benedict:
    BMR man = 88.362 + ( 13.397 x vikt ) + ( 4.799 x längd ) - ( 5.677 x ålder );
    BMR kvinna = 447.593 + ( 9.247 x vikt ) + ( 3.098 x längd ) - (4.330 x ålder );
Vikt anges i kg, längd i cm och ålder i år.

Schofield:
    Ålder (år)	Ekvation (kcal/dag)			Standardavvikelse
 - män:
    < 3	            59.512 × W - 30.4			70
    3–10	            22.706 × W + 504.3			67
    10–18	            17.686 × W + 658.2			105
    18–30	            15.057 × W + 692.2			153
    30–60	            11.472 × W + 873.1			167
    >60	            11.711 × W + 587.7			164
 - kvinnor:
    < 3	            58.317 × W - 31.1			59
    3–10	            20.315 × W + 485.9			70
    10–18	            13.384 × W + 692.6			111
    18–30	            14.818 × W + 486.6			119
    30–60	            8.126 × W + 845.6			111
    >60	            9.082 × W + 658.5			108
- W = vikt, anges i kg.
- Standardavvikelsen representerar ett konfidensintervall (+/-) för Schofields basalmetabolism, vilket gör att beräknat värde  justeras för smala och personer i nedre delen av aktuellt åldersintervall (+), och även för överviktiga och personer i övre delen av aktuella åldersintervallet (-). 

Mifflin St Jeor:
    RMR man = 9.99 x vikt + 6.25 x längd - 4.92 x ålder + 5;
    RMR kvinna = 9.99 x vikt + 6.25 x längd - 4.92 x ålder - 161;
Vikt anges i kg, längd i cm och ålder i år.

Katch-McArdle/Cunningham:
    RDEE = 370 + (21.6 x LBM)
LBM = lean body mass angett i kg.

Basalmetabolism kan blir över- eller underskattat med upp till 20% beroende på mängd kroppsfett, träningsnivå, stress, sjukdom, drogbruk, födointag, altitude, temperatur och menopaus.
">
			>
		>
		lifecycle_state = <"Author draft">
		original_author = <
			["date"] = <"2017-01-09">
			["email"] = <"models@cambiocds.com">
			["name"] = <"Eneimi Allwell-Brown">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_contributors = <"Dennis Forslund, Cambio Healthcare Systems", "Jimmy Axelsson, Cambio Healthcare Systems">
		other_details = <
			["references"] = <"Harris JA, Benedict FG. A biometric study of human basal metabolism. Proceedings of the National Academy of Sciences. 1918 Dec 1;4(12):370-3.

Roza AM, Shizgal HM. The Harris Benedict equation reevaluated: resting energy requirements and the body cell mass. The American journal of clinical nutrition. 1984 Jul 1;40(1):168-82.

Schofield WN. Predicting basal metabolic rate, new standards and review of previous work. Human nutrition. Clinical nutrition. 1984 Dec;39:5-41.

Mifflin MD, St Jeor ST, Hill LA, Scott BJ, Daugherty SA, Koh YO. A new predictive equation for resting energy expenditure in healthy individuals. The American journal of clinical nutrition. 1990 Feb 1;51(2):241-7.

Henry CJ. Basal metabolic rate studies in humans: measurement and development of new equations. Public health nutrition. 2005 Oct 1;8(7a):1133-52.

McArdle WD, Katch FI, Katch VL. Essentials of exercise physiology. Lippincott Williams & Wilkins; 2006.">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0002"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.body_weight.v1">
				domain = <"EHR">
				elements = <
					["gt0003"] = (ELEMENT_BINDING) <
						path = <"/data[at0002]/events[at0003]/data[at0001]/items[at0004]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0004"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.height.v1">
				domain = <"EHR">
				elements = <
					["gt0005"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0006"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.basic_demographic.v1">
				domain = <"EHR">
				elements = <
					["gt0007"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
					["gt0008"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0008]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0010"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.basal_metabolic_rate.v1">
				domain = <"CDS">
				elements = <
					["gt0011"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
					["gt0012"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0005]">
					>
					["gt0013"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0006]">
					>
					["gt0014"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0007]">
					>
					["gt0015"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0008]">
					>
					["gt0042"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0010]">
					>
				>
			>
			["gt0023"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.lean_body_mass.v1">
				domain = <"CDS">
				elements = <
					["gt0024"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0006]">
					>
					["gt0029"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0007]">
					>
				>
			>
		>
		rules = <
			["gt0009"] = (RULE) <
				when = <"$gt0005.units=='cm'", "$gt0003.units=='kg'", "$gt0007==local::at0005|Male|">
				then = <"$gt0011.units='kcal/d'", "$gt0011.magnitude=((((13.7516*$gt0003.magnitude)+(5.0033*$gt0005.magnitude))-(6.7550*($currentDateTime.year-$gt0008.year)))+66.4730)">
				priority = <20>
			>
			["gt0016"] = (RULE) <
				when = <"$gt0005.units=='cm'", "$gt0003.units=='kg'", "$gt0007==local::at0006|Female|">
				then = <"$gt0011.units='kcal/d'", "$gt0011.magnitude=((((9.5634*$gt0003.magnitude)+(1.8496*$gt0005.magnitude))-(4.6756*($currentDateTime.year-$gt0008.year)))+655.0955)">
				priority = <19>
			>
			["gt0017"] = (RULE) <
				when = <"$gt0005.units=='cm'", "$gt0003.units=='kg'", "$gt0007==local::at0005|Male|">
				then = <"$gt0013.units='kcal/d'", "$gt0013.magnitude=((((13.397*$gt0003.magnitude)+(4.799*$gt0005.magnitude))-(5.677*($currentDateTime.year-$gt0008.year)))+88.362)">
				priority = <18>
			>
			["gt0018"] = (RULE) <
				when = <"$gt0005.units=='cm'", "$gt0003.units=='kg'", "$gt0007==local::at0006|Female|">
				then = <"$gt0013.units='kcal/d'", "$gt0013.magnitude=((((9.247*$gt0003.magnitude)+(3.098*$gt0005.magnitude))-(4.330*($currentDateTime.year-$gt0008.year)))+447.593)">
				priority = <17>
			>
			["gt0019"] = (RULE) <
				when = <"$gt0005.units=='cm'", "$gt0003.units=='kg'", "$gt0007==local::at0005|Male|">
				then = <"$gt0014.units='kcal/d'", "$gt0014.magnitude=((((9.99*$gt0003.magnitude)+(6.25*$gt0005.magnitude))-(4.92*($currentDateTime.year-$gt0008.year)))+5)">
				priority = <16>
			>
			["gt0020"] = (RULE) <
				when = <"$gt0005.units=='cm'", "$gt0003.units=='kg'", "$gt0007==local::at0006|Female|">
				then = <"$gt0014.units='kcal/d'", "$gt0014.magnitude=((((9.99*$gt0003.magnitude)+(6.25*$gt0005.magnitude))-(4.92*($currentDateTime.year-$gt0008.year)))-161)">
				priority = <15>
			>
			["gt0021"] = (RULE) <
				when = <"$gt0024!=null",...>
				then = <"$gt0015.units='kcal/d'", "$gt0015.magnitude=(370+(21.6*$gt0024.magnitude))">
				priority = <14>
			>
			["gt0025"] = (RULE) <
				when = <"$gt0008.value<($currentDateTime.value-14,a)", "$gt0005.units=='cm'", "$gt0003.units=='kg'", "$gt0007==local::at0005|Male|">
				then = <"$gt0024.units='kg'", "$gt0024.precision=2", "$gt0024.magnitude=(((0.32810*$gt0003.magnitude)+(0.33929*$gt0005.magnitude))-29.5336)">
				priority = <22>
			>
			["gt0026"] = (RULE) <
				when = <"$gt0008.value<($currentDateTime.value-14,a)", "$gt0005.units=='cm'", "$gt0003.units=='kg'", "$gt0007==local::at0006|Female|">
				then = <"$gt0024.units='kg'", "$gt0024.precision=2", "$gt0024.magnitude=(((0.29569*$gt0003.magnitude)+(0.41813*$gt0005.magnitude))-43.2933)">
				priority = <21>
			>
			["gt0027"] = (RULE) <
				when = <"$gt0005.units=='cm'", "$gt0003.units=='kg'", "$gt0008.value>=($currentDateTime.value-14,a)">
				then = <"$gt0029.units='kg'", "$gt0029.precision=2", "$gt0029.magnitude=(((3.8*0.0215)*($gt0003.magnitude^0.6469))*($gt0005.magnitude^0.7236))">
				priority = <23>
			>
			["gt0028"] = (RULE) <
				when = <"$gt0029!=null",...>
				then = <"$gt0015.units='kcal/d'", "$gt0015.magnitude=(370+(21.6*$gt0029.magnitude))">
				priority = <13>
			>
			["gt0030"] = (RULE) <
				when = <"$gt0003.units=='kg'", "$gt0007==local::at0005|Male|", "$gt0008.value>($currentDateTime.value-3,a)">
				then = <"$gt0042=70", "$gt0012.units='kcal/d'", "$gt0012.magnitude=((59.512*$gt0003.magnitude)-30.4)">
				priority = <12>
			>
			["gt0031"] = (RULE) <
				when = <"$gt0003.units=='kg'", "$gt0008.value<=($currentDateTime.value-3,a)", "$gt0007==local::at0005|Male|", "$gt0008.value>=($currentDateTime.value-10,a)">
				then = <"$gt0042=67", "$gt0012.units='kcal/d'", "$gt0012.magnitude=((22.706*$gt0003.magnitude)+504.3)">
				priority = <11>
			>
			["gt0032"] = (RULE) <
				when = <"$gt0003.units=='kg'", "$gt0007==local::at0005|Male|", "$gt0008.value<=($currentDateTime.value-11,a)", "$gt0008.value>=($currentDateTime.value-18,a)">
				then = <"$gt0042=105", "$gt0012.units='kcal/d'", "$gt0012.magnitude=((17.686*$gt0003.magnitude)+658.2)">
				priority = <10>
			>
			["gt0033"] = (RULE) <
				when = <"$gt0003.units=='kg'", "$gt0008.value<=($currentDateTime.value-19,a)", "$gt0007==local::at0005|Male|", "$gt0008.value>=($currentDateTime.value-30,a)">
				then = <"$gt0042=153", "$gt0012.units='kcal/d'", "$gt0012.magnitude=((15.057*$gt0003.magnitude)+692.2)">
				priority = <9>
			>
			["gt0034"] = (RULE) <
				when = <"$gt0003.units=='kg'", "$gt0007==local::at0005|Male|", "$gt0008.value<=($currentDateTime.value-31,a)", "$gt0008.value>=($currentDateTime.value-60,a)">
				then = <"$gt0042=167", "$gt0012.units='kcal/d'", "$gt0012.magnitude=((11.472*$gt0003.magnitude)+873.1)">
				priority = <8>
			>
			["gt0035"] = (RULE) <
				when = <"$gt0003.units=='kg'", "$gt0007==local::at0005|Male|", "$gt0008.value<($currentDateTime.value-60,a)">
				then = <"$gt0042=164", "$gt0012.units='kcal/d'", "$gt0012.magnitude=((11.711*$gt0003.magnitude)+587.7)">
				priority = <7>
			>
			["gt0036"] = (RULE) <
				when = <"$gt0008.value>($currentDateTime.value-3,a)", "$gt0007==local::at0006|Female|", "$gt0003.units=='kg'">
				then = <"$gt0042=59", "$gt0012.units='kcal/d'", "$gt0012.magnitude=((58.317*$gt0003.magnitude)-31.1)">
				priority = <6>
			>
			["gt0037"] = (RULE) <
				when = <"$gt0008.value<=($currentDateTime.value-3,a)", "$gt0008.value>=($currentDateTime.value-10,a)", "$gt0007==local::at0006|Female|", "$gt0003.units=='kg'">
				then = <"$gt0042=70", "$gt0012.units='kcal/d'", "$gt0012.magnitude=((20.315*$gt0003.magnitude)+485.9)">
				priority = <5>
			>
			["gt0038"] = (RULE) <
				when = <"$gt0008.value<=($currentDateTime.value-11,a)", "$gt0008.value>=($currentDateTime.value-18,a)", "$gt0007==local::at0006|Female|", "$gt0003.units=='kg'">
				then = <"$gt0042=111", "$gt0012.units='kcal/d'", "$gt0012.magnitude=((13.384*$gt0003.magnitude)+692.6)">
				priority = <4>
			>
			["gt0039"] = (RULE) <
				when = <"$gt0008.value<=($currentDateTime.value-19,a)", "$gt0008.value>=($currentDateTime.value-30,a)", "$gt0007==local::at0006|Female|", "$gt0003.units=='kg'">
				then = <"$gt0042=119", "$gt0012.units='kcal/d'", "$gt0012.magnitude=((14.818*$gt0003.magnitude)+486.6)">
				priority = <3>
			>
			["gt0040"] = (RULE) <
				when = <"$gt0008.value<=($currentDateTime.value-31,a)", "$gt0008.value>=($currentDateTime.value-60,a)", "$gt0007==local::at0006|Female|", "$gt0003.units=='kg'">
				then = <"$gt0042=111", "$gt0012.units='kcal/d'", "$gt0012.magnitude=((8.126*$gt0003.magnitude)+845.6)">
				priority = <2>
			>
			["gt0041"] = (RULE) <
				when = <"$gt0008.value<($currentDateTime.value-60,a)", "$gt0007==local::at0006|Female|", "$gt0003.units=='kg'">
				then = <"$gt0042=108", "$gt0012.units='kcal/d'", "$gt0012.magnitude=((9.082*$gt0003.magnitude)+658.5)">
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Basal Metabolic Rate (BMR) Calculator">
						description = <"Basal metabolic rate (BMR) is the amount of energy (kcal/day) that a person needs to keep the body functioning at rest. It is estimated from different equations using weight (kg), height (cm), age (years) and gender (male vs female). The Harris–Benedict, revised Harris–Benedict, and Mifflin St Jeor equations use weight, height, age and gender; the Schofield equations use weight, age and gender; while the Katch-McArdle/Cunningham equation uses lean body mass (LBM) only.">
					>
					["gt0003"] = (TERM) <
						text = <"Weight">
						description = <"The weight of the individual.">
					>
					["gt0005"] = (TERM) <
						text = <"Height/Length">
						description = <"The length of the body from crown of head to sole of foot.">
					>
					["gt0007"] = (TERM) <
						text = <"Gender">
						description = <"The patient's gender.">
					>
					["gt0008"] = (TERM) <
						text = <"Birthdate">
						description = <"The patient's date of birth.">
					>
					["gt0009"] = (TERM) <
						text = <"Calculate Harris-Benedict BMR (male)">
					>
					["gt0011"] = (TERM) <
						text = <"Harris–Benedict BMR">
						description = <"Estimated Basal Metabolic Rate (BMR) based on the Harris-Benedict equations.">
					>
					["gt0012"] = (TERM) <
						text = <"Schofield BMR">
						description = <"Estimated Basal Metabolic Rate (BMR) based on the Schofield equations.">
					>
					["gt0013"] = (TERM) <
						text = <"(Revised) Harris-Benedict BMR">
						description = <"Estimated Basal Metabolic Rate (BMR) based on the revised Harris-Benedict equations.">
					>
					["gt0014"] = (TERM) <
						text = <"Mifflin St Jeor BMR">
						description = <"Predicted Resting Metabolic Rate (RMR) based on the Mifflin St Jeor equations.">
					>
					["gt0015"] = (TERM) <
						text = <"Katch-McArdle/Cunningham BMR">
						description = <"Predicted Resting Daily Energy Expenditure (RDEE) based on the Katch-McArdle and Cunningham equations.">
					>
					["gt0016"] = (TERM) <
						text = <"Calculate Harris-Benedict BMR (female)">
					>
					["gt0017"] = (TERM) <
						text = <"Calculate (Revised) Harris-Benedict BMR (male)">
					>
					["gt0018"] = (TERM) <
						text = <"Calculate (Revised) Harris-Benedict BMR (female)">
					>
					["gt0019"] = (TERM) <
						text = <"Calculate Mifflin St Jeor RMR (male)">
					>
					["gt0020"] = (TERM) <
						text = <"Calculate Mifflin St Jeor RMR (female)">
					>
					["gt0021"] = (TERM) <
						text = <"Calculate Katch-McArdle/Cunningham RDEE with Hume eLBM">
					>
					["gt0024"] = (TERM) <
						text = <"Hume eLBM">
						description = <"Estimated lean body mass with Hume Formula.">
					>
					["gt0025"] = (TERM) <
						text = <"Calculate Hume eLBM (male)">
					>
					["gt0026"] = (TERM) <
						text = <"Calculate Hume eLBM (female)">
					>
					["gt0027"] = (TERM) <
						text = <"Calculate Peters eLBM (children <= 14 years)">
					>
					["gt0028"] = (TERM) <
						text = <"Calculate Katch-McArdle/Cunningham RDEE with Peters eLBM (children <= 14 years)">
					>
					["gt0029"] = (TERM) <
						text = <"Peters eLBM">
						description = <"Estimated lean body mass with Peters Formula.">
					>
					["gt0030"] = (TERM) <
						text = <"Calculate Schofield BMR (male < 3 years)">
					>
					["gt0031"] = (TERM) <
						text = <"Calculate Schofield BMR (male 3 - 10 years)">
					>
					["gt0032"] = (TERM) <
						text = <"Calculate Schofield BMR (male 11 - 18 years)">
					>
					["gt0033"] = (TERM) <
						text = <"Calculate Schofield BMR (male 19 - 30 years)">
					>
					["gt0034"] = (TERM) <
						text = <"Calculate Schofield BMR (male 31 - 60 years)">
					>
					["gt0035"] = (TERM) <
						text = <"Calculate Schofield BMR (male > 60 years)">
					>
					["gt0036"] = (TERM) <
						text = <"Calculate Schofield BMR (female < 3 years)">
					>
					["gt0037"] = (TERM) <
						text = <"Calculate Schofield BMR (female 3 - 10 years)">
					>
					["gt0038"] = (TERM) <
						text = <"Calculate Schofield BMR (female 11 - 18 years)">
					>
					["gt0039"] = (TERM) <
						text = <"Calculate Schofield BMR (female 19 - 30 years)">
					>
					["gt0040"] = (TERM) <
						text = <"Calculate Schofield BMR (female 31 - 60 years)">
					>
					["gt0041"] = (TERM) <
						text = <"Calculate Schofield BMR (female > 60 years)">
					>
					["gt0042"] = (TERM) <
						text = <"Schofield SEE">
						description = <"Standard error of estimation (SEE) associated with the Schofield equations.">
					>
				>
			>
			["sv"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Basalmetabolism (Basal Metabolic Rate - BMR)">
						description = <"Basalmetabolism (Basal Metabolic Rate - BMR) är den mängd energy (kcal/dag) en person behöver för att bevara kroppslig funktion i vila. Denna kan uppskattas med hjälp av olika formler baserade på vikt, längd, ålder och kön. Harris-Benedict, reviderade Harris-Benedict och Mifflin St Jeor är formler som använder samtliga dessa parametrar medan Schofield använder vikt, ålder och kön, och Katch-McArdle/Cunningham använder sig av endast ”Lean Body Mass – LBM\". ">
					>
					["gt0003"] = (TERM) <
						text = <"Vikt">
						description = <"*(en) The weight of the individual.">
					>
					["gt0005"] = (TERM) <
						text = <"Längd">
						description = <"*(en) The length of the body from crown of head to sole of foot.">
					>
					["gt0007"] = (TERM) <
						text = <"Kön">
						description = <"*(en) The patient's gender.">
					>
					["gt0008"] = (TERM) <
						text = <"Födelsedatum">
						description = <"*(en) The patient's date of birth.">
					>
					["gt0009"] = (TERM) <
						text = <"Beräkna Harris-Benedict BMR för män">
					>
					["gt0011"] = (TERM) <
						text = <"Harris–Benedict BMR">
						description = <"*(en) Estimated Basal Metabolic Rate (BMR) based on the Harris-Benedict equations.">
					>
					["gt0012"] = (TERM) <
						text = <"Schofield BMR">
						description = <"*(en) Estimated Basal Metabolic Rate (BMR) based on the Schofield equations.">
					>
					["gt0013"] = (TERM) <
						text = <"(Revised) Harris-Benedict BMR">
						description = <"*(en) Estimated Basal Metabolic Rate (BMR) based on the revised Harris-Benedict equations.">
					>
					["gt0014"] = (TERM) <
						text = <"Mifflin St Jeor BMR">
						description = <"*(en) Predicted Resting Metabolic Rate (RMR) based on the Mifflin St Jeor equations.">
					>
					["gt0015"] = (TERM) <
						text = <"Katch-McArdle/Cunningham BMR">
						description = <"*(en) Predicted Resting Daily Energy Expenditure (RDEE) based on the Katch-McArdle and Cunningham equations.">
					>
					["gt0016"] = (TERM) <
						text = <"Beräkna Harris-Benedict BMR för kvinnor">
					>
					["gt0017"] = (TERM) <
						text = <"Beräkna (Revised) Harris-Benedict BMR för män">
					>
					["gt0018"] = (TERM) <
						text = <"Beräkna (Revised) Harris-Benedict BMR för kvinnor">
					>
					["gt0019"] = (TERM) <
						text = <"Beräkna Mifflin St Jeor RMR för män">
					>
					["gt0020"] = (TERM) <
						text = <"Beräkna Mifflin St Jeor RMR för kvinnor">
					>
					["gt0021"] = (TERM) <
						text = <"Beräkna Katch-McArdle/Cunningham RDEE med Hume eLBM">
					>
					["gt0024"] = (TERM) <
						text = <"Hume eLBM">
						description = <"*(en) Estimated lean body mass with Hume Formula.">
					>
					["gt0025"] = (TERM) <
						text = <"Beräkna Hume eLBM (man)">
					>
					["gt0026"] = (TERM) <
						text = <"Beräkna Hume eLBM (kvinna)">
					>
					["gt0027"] = (TERM) <
						text = <"Beräkna Peters eLBM (barn <= 14 år)">
					>
					["gt0028"] = (TERM) <
						text = <"Beräkna Katch-McArdle/Cunningham RDEE med Peters eLBM(barn <= 14 år)">
					>
					["gt0029"] = (TERM) <
						text = <"Peters eLBM">
						description = <"*(en) Estimated lean body mass with Peters Formula.">
					>
					["gt0030"] = (TERM) <
						text = <"Beräkna Schofield BMR (man < 3 år)">
					>
					["gt0031"] = (TERM) <
						text = <"Beräkna Schofield BMR (man 3 - 10 år)">
					>
					["gt0032"] = (TERM) <
						text = <"Beräkna Schofield BMR (man 11 - 18 år)">
					>
					["gt0033"] = (TERM) <
						text = <"Beräkna Schofield BMR (man 19 - 30 år)">
					>
					["gt0034"] = (TERM) <
						text = <"Beräkna Schofield BMR (man 31 - 60 år)">
					>
					["gt0035"] = (TERM) <
						text = <"Beräkna Schofield BMR (man > 60 år)">
					>
					["gt0036"] = (TERM) <
						text = <"Beräkna Schofield BMR (kvinna < 3 år)">
					>
					["gt0037"] = (TERM) <
						text = <"Beräkna Schofield BMR (kvinna 3 - 10 år)">
					>
					["gt0038"] = (TERM) <
						text = <"Beräkna Schofield BMR (kvinna 11 - 18 år)">
					>
					["gt0039"] = (TERM) <
						text = <"Beräkna Schofield BMR (kvinna 19 - 30 år)">
					>
					["gt0040"] = (TERM) <
						text = <"Beräkna Schofield BMR (kvinna 31 - 60 år)">
					>
					["gt0041"] = (TERM) <
						text = <"Beräkna Schofield BMR (kvinna > 60 år)">
					>
					["gt0042"] = (TERM) <
						text = <"Schofield SEE">
						description = <"*(en) Standard error of estimation (SEE) associated with the Schofield equations.">
					>
				>
			>
		>
	>
>
