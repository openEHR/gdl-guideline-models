(GUIDE) <
	gdl_version = <"0.1">
	id = <"CKD-EPI_equation.v1">
	concept = <"gt0000">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"Cambio Healthcare Systems">
				keywords = <"kidney function", "glomerular filtration rate", "GFR", "creatinine", "kidney", "CKD-EPI formula">
				misuse = <"Do not use for calculating eGFR based on other formulas.">
				purpose = <"To calculate estimated glomerular filtration rate based on serum creatine lab test result according to the CKD-EPI formula, in males and females.">
				use = <"Use for calculating eGFR based on CKD-EPI formula.">
			>
			["sv"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"Cambio Healthcare Systems">
				keywords = <"CKD-EPI", "CKD", "EPI", "eGFR", "glomerulär", "gfr">
				misuse = <"Använd inte för att beräkna eGFR med andra formler än CKD-EPI.">
				purpose = <"Att beräkna den glomerulära filtrationshastigheten genom användandet av labbvärdet serumkreatinin i enlighet med CKD-EPI formeln hos män och kvinnor.">
				use = <"Använd för att beräkna uppskattad glomerulär filtrationshastighet för män och kvinnor enligt CKD-EPI formeln.

">
			>
		>
		lifecycle_state = <"Author draft">
		original_author = <
			["date"] = <"2016-06-21">
			["email"] = <"rong.chen@cambio.se">
			["name"] = <"Rong Chen">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_contributors = <"Oskar Nielsen", "Dennis Forslund", "Jimmy Axelsson">
		other_details = <
			["references"] = <"Levey et al. A New Equation to Estimate Glomerular Filtration Rate. Ann Intern Med. 2009;150:604-612.">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0009"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.basic_demographic.v1">
				domain = <"EHR">
				elements = <
					["gt0002"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
					["gt0008"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0008]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0010"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.lab_test-urea_and_electrolytes.v1">
				domain = <"EHR">
				elements = <
					["gt0003"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0078.7]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0011"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.estimated_glomerular_filtration_rate.v1">
				domain = <"CDS">
				elements = <
					["gt0007"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
					["gt0012"] = (ELEMENT_BINDING) <
						path = <"/protocol[at0005]/items[at0010]">
					>
				>
			>
		>
		rules = <
			["gt0001"] = (RULE) <
				when = <"$gt0002==local::at0006|Female|", "$gt0003<=62,umol/l">
				then = <"$gt0012=local::at0015|CKD-EPI formula|", "$gt0007.magnitude=((144*((($gt0003.magnitude*0.011312)/0.7)^(-0.329)))*(0.993^($currentDateTime.year-$gt0008.year)))", "$gt0007.precision=2", "$gt0007.units='ml/min'">
				priority = <1>
			>
			["gt0004"] = (RULE) <
				when = <"$gt0002==local::at0006|Female|", "$gt0003>62,umol/l">
				then = <"$gt0012=local::at0015|CKD-EPI formula|", "$gt0007.magnitude=((144*((($gt0003.magnitude*0.011312)/0.7)^(-1.209)))*(0.993^($currentDateTime.year-$gt0008.year)))", "$gt0007.precision=2", "$gt0007.units='ml/min'">
				priority = <2>
			>
			["gt0005"] = (RULE) <
				when = <"$gt0002==local::at0005|Male|", "$gt0003<=80,umol/l">
				then = <"$gt0012=local::at0015|CKD-EPI formula|", "$gt0007.magnitude=((141*((($gt0003.magnitude*0.011312)/0.9)^(-0.411)))*(0.993^($currentDateTime.year-$gt0008.year)))", "$gt0007.units='ml/min'", "$gt0007.precision=2">
				priority = <3>
			>
			["gt0006"] = (RULE) <
				when = <"$gt0002==local::at0005|Male|", "$gt0003>80,umol/l">
				then = <"$gt0012=local::at0015|CKD-EPI formula|", "$gt0007.magnitude=((141.0*((($gt0003.magnitude*0.011312)/0.9)^(-1.209)))*(0.993^($currentDateTime.year-$gt0008.year)))", "$gt0007.precision=2", "$gt0007.units='ml/min'">
				priority = <4>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0000"] = (TERM) <
						text = <"CKD-EPI GFR Calculation">
						description = <"Calculation of glomerular filtration rate (GFR) to assess renal function using the CKD-EPI equation [1].">
					>
					["gt0001"] = (TERM) <
						text = <"Female low creatinine">
					>
					["gt0002"] = (TERM) <
						text = <"Gender">
						description = <"*">
					>
					["gt0003"] = (TERM) <
						text = <"Creatinine">
						description = <"Creatinine level in this specimen">
					>
					["gt0004"] = (TERM) <
						text = <"Female high creatinine">
					>
					["gt0005"] = (TERM) <
						text = <"Male low creatinine">
					>
					["gt0006"] = (TERM) <
						text = <"Male high creatinine">
					>
					["gt0007"] = (TERM) <
						text = <"Estimated GFR">
						description = <"*">
					>
					["gt0008"] = (TERM) <
						text = <"Birthdate">
						description = <"*">
					>
					["gt0012"] = (TERM) <
						text = <"Formula">
						description = <"Formula used to calculate the Estimated Glomerular Filtration Rate.">
					>
				>
			>
			["sv"] = (TERM_DEFINITION) <
				terms = <
					["gt0000"] = (TERM) <
						text = <"CKD-EPI eGFR Beräkning">
						description = <"Beräkning av en estimerad glomerulär filtrationshastighet (GFR) med CKD-EPI ekvationen [1].">
					>
					["gt0001"] = (TERM) <
						text = <"Kvinna med lågt kreatinin">
					>
					["gt0002"] = (TERM) <
						text = <"Kön">
						description = <"">
					>
					["gt0003"] = (TERM) <
						text = <"Kreatinin">
						description = <"Kreatininnivå i detta prov.">
					>
					["gt0004"] = (TERM) <
						text = <"Kvinna med högt kreatinin">
					>
					["gt0005"] = (TERM) <
						text = <"Man med lågt kreatinin">
					>
					["gt0006"] = (TERM) <
						text = <"Man med högt kreatinin">
					>
					["gt0007"] = (TERM) <
						text = <"Beräknat GFR">
						description = <"">
					>
					["gt0008"] = (TERM) <
						text = <"Födelsedatum">
						description = <"">
					>
					["gt0012"] = (TERM) <
						text = <"Formel">
						description = <"Formel som används för att beräkna den estimerade glomerulära filtrationshastigheten.">
					>
				>
			>
		>
	>
>
