(GUIDE) <
	gdl_version = <"0.1">
	id = <"Calculated_age.v1.0.0">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				keywords = <"age",...>
			>
		>
		original_author = <
			["name"] = <"Rong Chen">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0002"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.basic_demographic.v1">
				domain = <"EHR">
				elements = <
					["gt0003"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0008]">
					>
				>
			>
			["gt0004"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.basic_demographic.v1">
				domain = <"CDS">
				elements = <
					["gt0005"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0013]">
					>
				>
			>
		>
		rules = <
			["gt0006"] = (RULE) <
				when = <"$gt0005==null", "$gt0003!=null">
				then = <"$gt0005.magnitude=(($currentDateTime.value-$gt0003.value)/1,a)", "$gt0005.precision=0", "$gt0005.units='a'">
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Calculated age">
						description = <"Calculate age based on given birth date">
					>
					["gt0003"] = (TERM) <
						text = <"Birthdate">
						description = <"The patient's date of birth.">
					>
					["gt0005"] = (TERM) <
						text = <"Age">
						description = <"Age in years, and for babies: months, weeks or days">
					>
					["gt0006"] = (TERM) <
						text = <"Calculate age">
					>
				>
			>
		>
	>
>
