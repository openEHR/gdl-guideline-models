(GUIDE) <
	gdl_version = <"0.1">
	id = <"DLCN_score_Assessment.v1">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"familial hypercholesterolaemia", "FH", "xanthoma", "arcus", "LDL-cholesterol", "genetic testing", "coronary heart disease">
				purpose = <"To assess the DLCN score and aid diagnosis of heterozygous Familial Hypercholesterolaemia (FH) in adults.">
				use = <"To assess the DLCN score and aid diagnosis of FH.
DLCN score > 8 = definite FH
DLCN score 6-8 = probable FH
DLCN score 3-5 = possible FH
DLCN score < 3 = unlikely FH.
Molecular genetic testing and referral to a lipid specialist is strongly recommended in individuals with DLCN score > 5 and particularly those with xanthoma and/or high cholesterol plus a family history of premature coronary heart disease. Molecular genetic testing should also be offered to all first degree relatives of individuals with a confirmed causative gene mutation. 

DLCN score is calculated by a separate application: DLCN_score_Calculation.v1">
			>
		>
		lifecycle_state = <"Author draft">
		original_author = <
			["date"] = <"2017-02-23">
			["email"] = <"models@cambiocds.com">
			["name"] = <"Eneimi Allwell-Brown">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_contributors = <"Dennis Forslund", "Jimmy Axelsson">
		other_details = <
			["references"] = <"Nordestgaard BG, Chapman MJ, Humphries SE, Ginsberg HN, Masana L, Descamps OS, Wiklund O, Hegele RA, Raal FJ, Defesche JC, Wiegman A. Familial hypercholesterolaemia is underdiagnosed and undertreated in the general population: guidance for clinicians to prevent coronary heart disease. European heart journal. 2013 Dec 1;34(45):3478-90.

Law MR, Wald NJ, Rudnicka AR. Quantifying effect of statins on low density lipoprotein cholesterol, ischaemic heart disease, and stroke: systematic review and meta-analysis. Bmj. 2003 Jun 26;326(7404):1423.">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0002"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.dutch_lipid_clinic_network_score.v1">
				domain = <"CDS">
				elements = <
					["gt0003"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0052]">
					>
				>
			>
			["gt0004"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-EVALUATION.dutch_lipid_clinic_network_score.v1">
				domain = <"CDS">
				elements = <
					["gt0005"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/items[at0002]">
					>
				>
			>
		>
		rules = <
			["gt0006"] = (RULE) <
				when = <"$gt0003>8",...>
				then = <"$gt0005=local::at0003|Definite familial hypercholesterolemia (FH)|",...>
				priority = <4>
			>
			["gt0007"] = (RULE) <
				when = <"$gt0003<=8", "$gt0003>=6">
				then = <"$gt0005=local::at0004|Probable familial hypercholesterolemia (FH)|",...>
				priority = <3>
			>
			["gt0008"] = (RULE) <
				when = <"$gt0003<=5", "$gt0003>=3">
				then = <"$gt0005=local::at0005|Possible familial hypercholesterolemia (FH)|",...>
				priority = <2>
			>
			["gt0009"] = (RULE) <
				when = <"$gt0003<3",...>
				then = <"$gt0005=local::at0006|Unlikely familial hypercholesterolemia (FH)|",...>
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Dutch Lipid Clinic Network (DLCN) Score Assessment">
						description = <"The Dutch Lipid Clinic Network (DLCN) score is derived from the responses to a set of 10 clinical and laboratory-based questions recommended by the European Atherosclerosis Society (EAS) for aiding the diagnosis of heterozygous Familial Hypercholesterolaemia (FH) in adults. The criteria fall into five categories: family history (2 points), clinical history (2 points), physical examination findings (6 points), pre-treatment LDL cholesterol level (8 points) and presence of causative mutation on DNA analysis (8 points); minimum score = 0 and maximum score = 26. DLCN score >8 is ‘definite FH’, score 6 - 8 is ‘probable FH', score 3 - 5 is ‘possible FH’ and score <3 is ‘unlikely FH’. Molecular genetic testing is strongly recommended with DLCN score >5 and in first degree relatives of individuals with confirmed causative gene mutation.">
					>
					["gt0003"] = (TERM) <
						text = <"DLCN score">
						description = <"Sum of points from DLCN criteria.">
					>
					["gt0005"] = (TERM) <
						text = <"DLCN score assessment">
						description = <"Assessment of the DLCN score and probability of a diagnosis of familial hypercholesterolemia.">
					>
					["gt0006"] = (TERM) <
						text = <"Set definite FH ">
					>
					["gt0007"] = (TERM) <
						text = <"Set probable FH">
					>
					["gt0008"] = (TERM) <
						text = <"Set possible FH">
					>
					["gt0009"] = (TERM) <
						text = <"Set unlikely FH">
					>
				>
			>
		>
	>
>
