(GUIDE) <
	gdl_version = <"0.1">
	id = <"Estimated_GFR_LM_Revised.v1">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"N/A">
				keywords = <"LM-r", "LM-rev", "eGFR", "GFR">
				misuse = <"Only use this guideline when intending to estimate GFR using LM-r.">
				purpose = <"The purpose of using this guideline is to estimate the GFR which may be used to evaluate renal function of patients.">
				use = <"">
			>
		>
		lifecycle_state = <"Initial">
		original_author = <
			["date"] = <"28/09/17">
			["email"] = <"nyberg.per@outlook.com">
			["name"] = <"Per Nyberg/Nathan Brown">
			["organisation"] = <"KI">
		>
		other_contributors = <"Nathan Brown", "Per Nyberg", "Jimmy Axelsson">
		other_details = <
			["references"] = <"Björk, J., Grubb, A., Sterner, G., & Nyman, U. (2011). Revised equations for estimating glomerular filtration rate based on the Lund-Malmö Study cohort. Scandinavian journal of clinical and laboratory investigation, 71(3), 232-239.">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0002"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.lab_test-urea_and_electrolytes.v1">
				domain = <"EHR">
				elements = <
					["gt0008"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0078.7]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0003"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.estimated_glomerular_filtration_rate.v1">
				domain = <"CDS">
				elements = <
					["gt0007"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
					["gt0009"] = (ELEMENT_BINDING) <
						path = <"/protocol[at0005]/items[at0010]">
					>
				>
			>
			["gt0004"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.basic_demographic_lm_rev.v1">
				domain = <"EHR">
				elements = <
					["gt0005"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0013]">
					>
					["gt0006"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
		>
		rules = <
			["gt0010"] = (RULE) <
				when = <"$gt0008>=180,umol/l", "$gt0006==local::at0005|Male|">
				then = <"$gt0007.magnitude=(round((e^(((2.56-(0.926*(log(($gt0008.magnitude/180)))))-(0.0158*$gt0005.magnitude))+(0.438*(log($gt0005.magnitude)))))))", "$gt0009=local::at0019|Other formula|", "$gt0007.precision=2", "$gt0007.units='ml/min'">
				priority = <4>
			>
			["gt0011"] = (RULE) <
				when = <"$gt0008<180,umol/l", "$gt0006==local::at0005|Male|">
				then = <"$gt0009=local::at0019|Other formula|", "$gt0007.magnitude=(round((e^(((2.56+(0.00968*(180-$gt0008.magnitude)))-(0.0158*$gt0005.magnitude))+(0.438*(log($gt0005.magnitude)))))))", "$gt0007.precision=2", "$gt0007.units='ml/min'">
				priority = <3>
			>
			["gt0012"] = (RULE) <
				when = <"$gt0006==local::at0006|Female|", "$gt0008>=150,umol/l">
				then = <"$gt0009=local::at0019|Other formula|", "$gt0007.magnitude=(round((e^(((2.5-(0.926*(log(($gt0008.magnitude/150)))))-(0.0158*$gt0005.magnitude))+(0.438*(log($gt0005.magnitude)))))))", "$gt0007.units='ml/min'", "$gt0007.precision=2">
				priority = <2>
			>
			["gt0013"] = (RULE) <
				when = <"$gt0006==local::at0006|Female|", "$gt0008<150,umol/l">
				then = <"$gt0007.units='ml/min'", "$gt0007.precision=2", "$gt0007.magnitude=(round((e^(((2.5+(0.0121*(150-$gt0008.magnitude)))-(0.0158*$gt0005.magnitude))+(0.438*(log($gt0005.magnitude)))))))", "$gt0009=local::at0019|Other formula|">
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"LM-r eGFR calculation">
						description = <"This is a guideline utlilizing LM-r (Lund Malmö revised) equation for estimating glomerular filtration rate in males and females.

eGFR = e^X–0.0158×Age+0.438×ln(Age)

Female pCr?<?150 mmol/L: X?=?2.50+0.0121?×?(150–pCr)
Female pCr?=?150 mmol/L: X?=?2.50–0.926?×?ln(pCr/150)
Male pCr?<?180 mmol/L: X?=?2.56+0.00968?×?(180–pCr)
Male pCr?=?180 mmol/L: X?=?2.56–0.926?×?ln(pCr/180)">
					>
					["gt0005"] = (TERM) <
						text = <"Age">
						description = <"Age in years, and for babies: months, weeks or days">
					>
					["gt0006"] = (TERM) <
						text = <"Gender">
						description = <"The patient's gender.">
					>
					["gt0007"] = (TERM) <
						text = <"Estimated Glomerular Filtration Rate">
						description = <"Value describing estimation of glomerular filtration rate adjusted by body-surface area (mL/min/1.73m²).">
					>
					["gt0008"] = (TERM) <
						text = <"Creatinine">
						description = <"Bicarbonate level in this specimen.">
					>
					["gt0009"] = (TERM) <
						text = <"Formula">
						description = <"Formula used to calculate the Estimated Glomerular Filtration Rate.">
					>
					["gt0010"] = (TERM) <
						text = <"male high pCr">
					>
					["gt0011"] = (TERM) <
						text = <"male low pCr">
					>
					["gt0012"] = (TERM) <
						text = <"female high pCr">
					>
					["gt0013"] = (TERM) <
						text = <"female low pCr">
					>
				>
			>
		>
	>
>
