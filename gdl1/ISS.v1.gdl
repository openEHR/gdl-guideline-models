(GUIDE) <
	gdl_version = <"0.1">
	id = <"ISS.v1">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"Cambio Healthcare Systems">
				keywords = <"injury severity", "ISS", "anatomical scoring system", "multiple injury assessment", "multiple trauma assessment", "AIS score", "NISS">
				misuse = <"Should not be based on preliminary diagnoses (suspected, possible, probable, rule out etc.).
Should not be based on complications, consequences or outcomes of the injury.
Not applicable to non-traumatic injury.">
				purpose = <"Calculates the Injury Severity Score which describes severity of injury in persons with multiple trauma.">
				use = <"Use to calculate the injury severity score (ISS) which is the sum of the squares of the highest abbreviated injury scale (AIS) score in each of the three most severely injured body regions. Injury severity score (ISS) summarizes injury severity and correlates well with survival following multiple injury. It also demonstrates the effects of additional injuries on mortality.

ISS = A*A + B*B + C*C, where A, B and C are the AIS scores assigned to the most severe injuries in the three most severely injured body regions. Possible AIS scores range from 0 = no injury to 5 = critical injury, while possible ISS ranges from 0 - 75. An unsurvivable injury (AIS score of 6) in any body region results in the individual automatically assigned an ISS of 75, as further medical care is thought to be futile in such cases.
More detail about scoring of the AIS is provided in the openEHR-EHR-OBSERVATION.abbreviated_injury_scale.v1 archetype.">
			>
			["sv"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"Cambio Healthcare Systems">
				keywords = <"injury severity score", "ISS", "AIS", "NISS", "trauma">
				misuse = <"Ej avsedd att användas för triage. Bedömning ska inte baseras på preliminär diagnos (misstänkt, möjlig, sannolik etc), och inte heller på komplikationer eller konsekvenser till följd av skadan. Ej applicerbar på icke trauma-patienter.">
				purpose = <"Att beräkna Injury Severity Score, vilket är ett anatomiskt baserat klassifikationssystem som används för att beskriva svårighetsgrad vid multipla skador.">
				use = <"Använd för att beräkna resultat genererade i enlighet med Injury Severity Score (ISS), vilken tar hänsyn till de tre mest skadade områdena på kroppen. ISS summerar svårighetsgraden av skadorna, och bygger på en formel enligt:

ISS = A*A + B*B + C*C, där samtliga faktorer hämtas från Abbreviated Injury Scale (AIS) vilken delar upp kroppen i nio områden och graderar aktuella skadorna på en skala 0-6. Endast de tre mest skadade områdena graderas, från 0 = ingen skada till 6 = inte förenlig med liv. Detta genererar en ISS-poäng om 0-75p. ISS har visats ha linjär korrelation med vårdtid, morbiditet och mortalitet hos traumapatienter.

En AIS-gradering om 6 genererar automatiskt en ISS-poäng om 75p, vilket innebär att skadorna är icke förenliga med liv. Mer information kring AIS återfinns i separat arketyp.
">
			>
		>
		lifecycle_state = <"Author draft">
		original_author = <
			["date"] = <"2016-10-02">
			["email"] = <"models@cambiocds.com">
			["name"] = <"Eneimi Allwell-Brown">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_contributors = <"Dennis Forslund", "Jimmy Axelsson">
		other_details = <
			["references"] = <"Joint Committee on Injury Scaling. The Abbreviated Injury Scale 1976 Revision. American Medical Association, Society of Automotive Engineers, and American Association for Automotive Medicine; Morton Grove, IL 60053, USA, 1976.

Baker SP, o'Neill B, Haddon Jr W, Long WB. The injury severity score: a method for describing patients with multiple injuries and evaluating emergency care. Journal of Trauma and Acute Care Surgery. 1974 Mar 1;14(3):187-96.">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0003"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.injury_severity_score.v1">
				domain = <"CDS">
				elements = <
					["gt0004"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
					["gt0005"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0006]">
					>
					["gt0006"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0007]">
					>
					["gt0007"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0008]">
					>
					["gt0016"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0005]">
					>
				>
			>
			["gt0008"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.injury_severity_score.v1">
				domain = <"EHR">
				elements = <
					["gt0009"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0006]">
					>
					["gt0010"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0007]">
					>
					["gt0011"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0008]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
		>
		rules = <
			["gt0002"] = (RULE) <
				when = <"$gt0004==null",...>
				then = <"$gt0004.magnitude=((($gt0005.value^2)+($gt0006.value^2))+($gt0007.value^2))",...>
				priority = <2>
			>
			["gt0012"] = (RULE) <
				when = <"$gt0009!=null",...>
				then = <"$gt0005=$gt0009",...>
				priority = <5>
			>
			["gt0013"] = (RULE) <
				when = <"$gt0010!=null",...>
				then = <"$gt0006=$gt0010",...>
				priority = <4>
			>
			["gt0014"] = (RULE) <
				when = <"$gt0011!=null",...>
				then = <"$gt0007=$gt0011",...>
				priority = <3>
			>
			["gt0015"] = (RULE) <
				when = <"$gt0009==6|local::at0015|Unsurvivable injury|",...>
				then = <"$gt0004=75", "$gt0016='Unsurvivable injury (AIS 6) detected.'">
				priority = <1>
			>
			["gt0017"] = (RULE) <
				when = <"$gt0005==null", "$gt0006==null", "$gt0007==null">
				then = <"$gt0007=0|local::at0023|No injury|", "$gt0006=0|local::at0016|No injury|", "$gt0005=0|local::at0009|No injury|">
				priority = <6>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Injury Severity Score Calculator">
						description = <"Anatomical based scoring system for numerical description of the severity of injury in persons who have sustained trauma to mutiple body regions. ISS = A*A + B*B + C*C, where A, B and C are the AIS (abbreviated injury scale) scores assigned to the most severe injuries in the three most severely injured body regions (head/neck, face, chest, abdomen, extremities and skin). Possible AIS scores range from 0 = no injury to 5 = critical injury, while possible ISS ranges from 0 - 75. An unsurvivable injury (AIS score of 6) in any body region results in the individual automatically assigned an ISS of 75 - an injury incompatible with life. ISS shows linear correlation with hospitalisation, morbidity and mortality following trauma.">
					>
					["gt0002"] = (TERM) <
						text = <"Calculate injury severity score">
						description = <"Contains the expression logic for calculating injurity severity score (ISS).">
					>
					["gt0004"] = (TERM) <
						text = <"Injury severity score">
						description = <"Calculated injury severity score. This is the sum of squares of the highest AIS scores in the three most severely injured body regions. ">
					>
					["gt0005"] = (TERM) <
						text = <"AIS score of most severely injured region">
						description = <"Abbreviated injury scale score of the most severely injured body region.">
					>
					["gt0006"] = (TERM) <
						text = <"AIS score of 2nd most severely injured region">
						description = <"Abbreviated injury scale score of the second most severely injured body region.">
					>
					["gt0007"] = (TERM) <
						text = <"AIS score of 3rd most severely injured region">
						description = <"Abbreviated injury scale score of the third most severely injured body region.">
					>
					["gt0009"] = (TERM) <
						text = <"AIS score of most severely injured region">
						description = <"Abbreviated injury scale score of the most severely injured body region.">
					>
					["gt0010"] = (TERM) <
						text = <"AIS score of 2nd most severely injured region">
						description = <"Abbreviated injury scale score of the second most severely injured body region.">
					>
					["gt0011"] = (TERM) <
						text = <"AIS score of 3rd most severely injured region">
						description = <"Abbreviated injury scale score of the third most severely injured body region.">
					>
					["gt0012"] = (TERM) <
						text = <"Set CDS for most severely injured region">
					>
					["gt0013"] = (TERM) <
						text = <"Set CDS for 2nd most severely injured region">
					>
					["gt0014"] = (TERM) <
						text = <"Set CDS for 3rd most severely injured region">
					>
					["gt0015"] = (TERM) <
						text = <"Unsurvivable injury">
					>
					["gt0016"] = (TERM) <
						text = <"Comment">
						description = <"*">
					>
					["gt0017"] = (TERM) <
						text = <"Set defaults">
					>
				>
			>
			["sv"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Injury Severity Score">
						description = <"Injury Severity Score (ISS) är ett anatomiskt baserat klassifikationssystem som används för att beskriva svårighetsgrad vid multipla skador.  ISS = A*A + B*B + C*C, där samtliga faktorer hämtas från Abbreviated Injury Scale (AIS) vilken delar upp kroppen i nio områden och graderar aktuella skadorna på en skala 0-6. Endast de tre mest skadade områdena graderas, från 0 = ingen skada till 6 = inte förenlig med liv. Detta genererar en ISS-poäng om 0-75p. ISS har visats ha linjär korrelation med vårdtid, morbiditet och mortalitet hos traumapatienter.">
					>
					["gt0002"] = (TERM) <
						text = <"Beräkna injury severity score">
						description = <"*(en) Contains the expression logic for calculating injurity severity score (ISS).">
					>
					["gt0004"] = (TERM) <
						text = <"Injury severity score">
						description = <"*(en) Calculated injury severity score. This is the sum of squares of the highest AIS scores in the three most severely injured body regions. ">
					>
					["gt0005"] = (TERM) <
						text = <"AIS första region">
						description = <"*(en) Abbreviated injury scale score of the most severely injured body region.">
					>
					["gt0006"] = (TERM) <
						text = <"AIS andra region">
						description = <"*(en) Abbreviated injury scale score of the second most severely injured body region.">
					>
					["gt0007"] = (TERM) <
						text = <"AIS tredje region">
						description = <"*(en) Abbreviated injury scale score of the third most severely injured body region.">
					>
					["gt0009"] = (TERM) <
						text = <"AIS första region">
						description = <"*(en) Abbreviated injury scale score of the most severely injured body region.">
					>
					["gt0010"] = (TERM) <
						text = <"AIS andra region">
						description = <"*(en) Abbreviated injury scale score of the second most severely injured body region.">
					>
					["gt0011"] = (TERM) <
						text = <"AIS tredje region">
						description = <"*(en) Abbreviated injury scale score of the third most severely injured body region.">
					>
					["gt0012"] = (TERM) <
						text = <"Sätt CDS för AIS första region">
					>
					["gt0013"] = (TERM) <
						text = <"Sätt CDS för AIS andra region">
					>
					["gt0014"] = (TERM) <
						text = <"Sätt CDS för AIS tredje region">
					>
					["gt0015"] = (TERM) <
						text = <"Skada icke förenlig med liv">
					>
					["gt0016"] = (TERM) <
						text = <"Kommentar">
						description = <"*(en) *">
					>
					["gt0017"] = (TERM) <
						text = <"Standard">
					>
				>
			>
		>
	>
>
