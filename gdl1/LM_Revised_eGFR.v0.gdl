(GUIDE) <
	gdl_version = <"0.1">
	id = <"LM_Revised_eGFR.v0">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"LM equation", "glomerular filtration rate", "creatinine clearance">
				misuse = <"Not for use in patients on dialysis or with traumatic spinal cord lesions, myelomeningocele or other disorders causing marked muscle wasting.">
				purpose = <"Calculation of relative GFR using LM Revised equation.">
				use = <"To calculate the relative eGFR using the LM Revised equation:

e^X−0.0158×age+0.438×ln(age)
and;
Female and pCr<150: X=2.50+0.0121×(150–pCr)
Female and pCr≥150: X=2.50−0.926×ln(pCr/150)
Male and pCr<180: X=2.56+0.00968×(180−pCr)
Male and pCr≥180: X=2.56−0.926×ln(pCr/180)
where; 
e = Euler's number (Napier's constant),
ln = natural logarithm,
pCr = plasma creatinine in umol/L,
age = patient's age in years">
			>
		>
		lifecycle_state = <"Author draft">
		original_author = <
			["date"] = <"2017-08-30">
			["email"] = <"models@cambiocds.com">
			["name"] = <"Eneimi Allwell-Brown">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_contributors = <"Iago Corbal, Cambio Healthcare Systems", "Konstantinos Kalliamvakos, Cambio Healthcare Systems">
		other_details = <
			["references"] = <"Nyman U, Grubb A, Larsson A, Hansson LO, Flodin M, Nordin G, Lindström V, Björk J. The revised Lund-Malmö GFR estimating equation outperforms MDRD and CKD-EPI across GFR, age and BMI intervals in a large Swedish population. Clinical Chemistry and Laboratory Medicine (CCLM). 2014 Jun 1;52(6):815-24.">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0002"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.lab_test-urea_and_electrolytes.v1">
				domain = <"EHR">
				elements = <
					["gt0003"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0078.7]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0004"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.basic_demographic.v1">
				domain = <"EHR">
				elements = <
					["gt0005"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
					["gt0006"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0008]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0007"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.estimated_glomerular_filtration_rate.v1">
				domain = <"CDS">
				elements = <
					["gt0008"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0020]">
					>
					["gt0009"] = (ELEMENT_BINDING) <
						path = <"/protocol[at0005]/items[at0010]">
					>
				>
			>
			["gt0011"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.lab_test-urea_and_electrolytes.v1">
				domain = <"CDS">
				elements = <
					["gt0012"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0078.7]">
					>
				>
			>
		>
		pre_conditions = <"$gt0006.year<=($currentDateTime.year-18)",...>
		rules = <
			["gt0010"] = (RULE) <
				when = <"$gt0003.units=='mg/dl'",...>
				then = <"$gt0012.units='umol/l'", "$gt0012.magnitude=($gt0003.magnitude*88.4)">
				priority = <6>
			>
			["gt0013"] = (RULE) <
				when = <"$gt0003.units=='umol/l'",...>
				then = <"$gt0012=$gt0003",...>
				priority = <5>
			>
			["gt0014"] = (RULE) <
				when = <"$gt0012<150,umol/l", "$gt0005==local::at0006|Female|">
				then = <"$gt0009=local::at0021|LM Revised|", "$gt0008.precision=0", "$gt0008.units='ml/min/1.73m2'", "$gt0008.magnitude=(e^(((2.50+(0.0121*(150-$gt0012.magnitude)))-(0.0158*($currentDateTime.year-$gt0006.year)))+(0.438*(log(($currentDateTime.year-$gt0006.year))))))">
				priority = <4>
			>
			["gt0015"] = (RULE) <
				when = <"$gt0012>=150,umol/l", "$gt0005==local::at0006|Female|">
				then = <"$gt0009=local::at0021|LM Revised|", "$gt0008.precision=0", "$gt0008.units='ml/min/1.73m2'", "$gt0008.magnitude=(e^(((2.50-(0.926*(log(($gt0012.magnitude/150)))))-(0.0158*($currentDateTime.year-$gt0006.year)))+(0.438*(log(($currentDateTime.year-$gt0006.year))))))">
				priority = <3>
			>
			["gt0016"] = (RULE) <
				when = <"$gt0012<180,umol/l", "$gt0005==local::at0005|Male|">
				then = <"$gt0009=local::at0021|LM Revised|", "$gt0008.precision=0", "$gt0008.units='ml/min/1.73m2'", "$gt0008.magnitude=(e^(((2.56+(0.00968*(180-$gt0012.magnitude)))-(0.0158*($currentDateTime.year-$gt0006.year)))+(0.438*(log(($currentDateTime.year-$gt0006.year))))))">
				priority = <2>
			>
			["gt0017"] = (RULE) <
				when = <"$gt0012>=180,umol/l", "$gt0005==local::at0005|Male|">
				then = <"$gt0009=local::at0021|LM Revised|", "$gt0008.precision=0", "$gt0008.units='ml/min/1.73m2'", "$gt0008.magnitude=(e^(((2.56-(0.926*(log(($gt0012.magnitude/180)))))-(0.0158*($currentDateTime.year-$gt0006.year)))+(0.438*(log(($currentDateTime.year-$gt0006.year))))))">
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Revised Lund-Malmö eGFR">
						description = <"Revised Lund-Malmö eGFR calculates the relative glomerular filtration rate (GFR) in adults ≥ 18 years and provides a superior estimate of the GFR compared with the MDRD, CKD-EPI and original LM equations. Input variables include gender, age (in years) and plasma creatinine value. GFR is calculated as ml/min/1.73m2.">
					>
					["gt0003"] = (TERM) <
						text = <"pCr">
						description = <"Creatinine level in this specimen">
					>
					["gt0005"] = (TERM) <
						text = <"Gender">
						description = <"*">
					>
					["gt0006"] = (TERM) <
						text = <"Birthdate">
						description = <"*">
					>
					["gt0008"] = (TERM) <
						text = <"Relative eGFR">
						description = <"Value describing estimation of glomerular filtration rate independent of body-surface area (mL/min/1.73m²).">
					>
					["gt0009"] = (TERM) <
						text = <"Formula">
						description = <"Formula used to calculate the Estimated Glomerular Filtration Rate.">
					>
					["gt0010"] = (TERM) <
						text = <"Convert pCR from mg/dl to umol/l">
					>
					["gt0012"] = (TERM) <
						text = <"pCr (umol/l)">
						description = <"Creatinine level in this specimen">
					>
					["gt0013"] = (TERM) <
						text = <"Set pCr (umol/l)">
					>
					["gt0014"] = (TERM) <
						text = <"Calculate LM Revised for Female pCr < 150 umol/L">
					>
					["gt0015"] = (TERM) <
						text = <"Calculate LM Revised for Female pCr ≥ 150 umol/L">
					>
					["gt0016"] = (TERM) <
						text = <"Calculate LM Revised for Male pCr < 180 umol/L">
					>
					["gt0017"] = (TERM) <
						text = <"Caclulate LM Revised for Male pCr ≥ 180 umol/L">
					>
				>
			>
		>
	>
>
