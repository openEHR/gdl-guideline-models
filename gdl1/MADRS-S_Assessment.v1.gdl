(GUIDE) <
	gdl_version = <"0.1">
	id = <"MADRS-S_Assessment.v1">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"MADRS-S",...>
				misuse = <"Results not to be used alone as a diagnostic tool.">
				purpose = <"Used to categorise the MADRS-S score according to the scores derived from the questionnaire">
				use = <"Categorise the scores of drepressive illness from the MADRS-S tool into:

None/very mild depression: 0-12
Mild: 13-19
Moderate: 20-34
Severe: 34-54
">
			>
		>
		original_author = <
			["email"] = <"models@cambiocds.com">
			["name"] = <"Oskar Neilsen">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_contributors = <"Syeeda S Farruque", "Jimmy Axelsson", "Dennis Forslund">
		other_details = <
			["references"] = <"Ref.1: Svanborg P, Asberg M: A comparison between the Beck Depression Inventory (BDI) and the self-rated version of the Montgomery-Asberg Depression Rating Scale (MADRS). J Affect Dis 2001, 64(2-3):203-16.

Ref.2: Svanborg, P. & Åsberg, M. (1994). A new self-rating scale for depression and anxiety states based om the comprehensive psyckopathological rating scale. ACTA Psychiatrica Scandinavia, 89(1), 21–28

Ref.3: Snaith, R.P., Harrop, F.M., Newby, D.A. (1986). Grade scores of the Montgomery-Åsberg Depression and Clinical Anxiety Scales. British Journal of Psychiatry, 148, 599-601.">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0002"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-EVALUATION.madrs_s_depression_assessment.v1">
				domain = <"CDS">
				elements = <
					["gt0004"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/items[at0006]">
					>
				>
			>
			["gt0003"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.madrs_s.v1">
				domain = <"EHR">
				elements = <
					["gt0005"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0076]">
					>
				>
			>
		>
		pre_conditions = <"$gt0005!=null",...>
		rules = <
			["gt0006"] = (RULE) <
				when = <"$gt0005>=0", "$gt0005<=12">
				then = <"$gt0004=local::at0007|None or very mild depression|",...>
				priority = <4>
			>
			["gt0007"] = (RULE) <
				when = <"$gt0005>=13", "$gt0005<=19">
				then = <"$gt0004=local::at0008|Mild depression|",...>
				priority = <3>
			>
			["gt0008"] = (RULE) <
				when = <"$gt0005>=20", "$gt0005<=34">
				then = <"$gt0004=local::at0009|Moderate depression|",...>
				priority = <2>
			>
			["gt0009"] = (RULE) <
				when = <"$gt0005>34", "$gt0005<=54">
				then = <"$gt0004=local::at0011|Severe depression|",...>
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"MADRS-S Assessment">
						description = <"Evaluation/Assessment of the MADRS-S score">
					>
					["gt0004"] = (TERM) <
						text = <"Grade of depression">
						description = <"*">
					>
					["gt0005"] = (TERM) <
						text = <"Total score">
						description = <"The calculated sum of all items.">
					>
					["gt0006"] = (TERM) <
						text = <"Set none or very mild depression">
					>
					["gt0007"] = (TERM) <
						text = <"Set mild depression">
					>
					["gt0008"] = (TERM) <
						text = <"Set moderate depression">
					>
					["gt0009"] = (TERM) <
						text = <"Set severe depression">
					>
				>
			>
		>
	>
>
