(GUIDE) <
	gdl_version = <"0.1">
	id = <"Modified_Fisher_Scale_SAH.v1">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <" aneurysmal subarachnoid hemorrhage (aSAH)", "Intraventricular Haemorrhage", "Modified fisher grading scale for sah">
				misuse = <"Do not apply the scale to SAH caused by trauma, arteriovenous malformations, cavernous angiomas, dural arteriovenous fistulae, cortical or sinus venous thromboses, mycotic aneurysms or septic emboli with haemorrhagic transformation

Do not use the score on its own for diagnostic purposes without supporting evidence. ">
				purpose = <"The Modified Fisher scale only applies to aneurysmal subarachnoid hemorrhage (aSAH) and provides a grade of SAH based on amount and type of blood on CT with associated vasospasm risk

">
				use = <"The total score is based on whether SAH is present or not (of either focal/diffuse thin or thick SAH and on the basis of whether IVH (Intraventricular Haemorrhage) was present or not. The range is from 0 to 4.

    Thin SAH is classified as < 1mm in depth.
    Thick SAH is classified as ≥ 1mm in depth

There are Grades 0 to IV with risk of symptomatic vasopasm as shown below:


Score interpretation:
Modified Fisher Grade 	Risk of symptomatic vasospasm
0 	0%
1 	6-24%
2 	15-33%
3 	33-35%
4 	34-40%">
			>
		>
		lifecycle_state = <"Author draft">
		original_author = <
			["date"] = <"2017-12-23">
			["email"] = <"models@cambiocds.com">
			["name"] = <"Syeeda S Farruque">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_details = <
			["references"] = <"Ref, 1: Frontera JA, Claassen J, Schmidt JM, Wartenberg KE, Temes R, Connolly ES, Loch Macdonald R, Mayer SA. Prediction of symptomatic vasospasm after subarachnoid haemorrhage: the modified Fisher scale. Neurosurgery. 2006; 58(7): 21-27.">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0002"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.modified_fisher_grading_scale_for_sah.v1">
				domain = <"CDS">
				elements = <
					["gt0006"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
					["gt0007"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0005]">
					>
					["gt0008"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0013]">
					>
				>
			>
			["gt0003"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.modified_fisher_grading_scale_for_sah.v1">
				domain = <"EHR">
				elements = <
					["gt0004"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
					["gt0005"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0005]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
		>
		rules = <
			["gt0009"] = (RULE) <
				when = <"$gt0004!=null",...>
				then = <"$gt0006=$gt0004",...>
				priority = <7>
			>
			["gt0010"] = (RULE) <
				when = <"$gt0005!=null",...>
				then = <"$gt0007=$gt0005",...>
				priority = <6>
			>
			["gt0011"] = (RULE) <
				when = <"$gt0007==0|local::at0006|No|", "$gt0006==0|local::at0008|No SAH present|">
				then = <"$gt0008=0",...>
				priority = <5>
			>
			["gt0012"] = (RULE) <
				when = <"$gt0006==1|local::at0009|Focal or diffuse thin SAH|", "$gt0007==0|local::at0006|No|">
				then = <"$gt0008=1",...>
				priority = <4>
			>
			["gt0013"] = (RULE) <
				when = <"$gt0006==1|local::at0009|Focal or diffuse thin SAH|", "$gt0007==1|local::at0007|Yes|">
				then = <"$gt0008=2",...>
				priority = <3>
			>
			["gt0014"] = (RULE) <
				when = <"$gt0006==2|local::at0010|Focal or diffuse thick SAH|", "$gt0007==0|local::at0006|No|">
				then = <"$gt0008=3",...>
				priority = <2>
			>
			["gt0015"] = (RULE) <
				when = <"$gt0006==2|local::at0010|Focal or diffuse thick SAH|", "$gt0007==1|local::at0007|Yes|">
				then = <"$gt0008=4",...>
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Modified fisher grading scale for sah">
						description = <"The Modified Fischer scale for SAH (Subarachnoid Haemorrhage) provides a grade of SAH based on amount and type of blood on CT with associated vasospasm risk">
					>
					["gt0004"] = (TERM) <
						text = <"Signs/Symptoms">
						description = <"Signs/Symptoms">
					>
					["gt0005"] = (TERM) <
						text = <"IVH presemt">
						description = <"Intraventricular hemorrhage (IVH) present">
					>
					["gt0006"] = (TERM) <
						text = <"Signs/Symptoms">
						description = <"Signs/Symptoms">
					>
					["gt0007"] = (TERM) <
						text = <"IVH presemt">
						description = <"Intraventricular hemorrhage (IVH) present">
					>
					["gt0008"] = (TERM) <
						text = <"Score result">
						description = <"*">
					>
					["gt0009"] = (TERM) <
						text = <"Set Symptoms/Signs">
					>
					["gt0010"] = (TERM) <
						text = <"Set IVH present">
					>
					["gt0011"] = (TERM) <
						text = <"Set Score 0">
					>
					["gt0012"] = (TERM) <
						text = <"Set Score 1">
					>
					["gt0013"] = (TERM) <
						text = <"Set Score 2">
					>
					["gt0014"] = (TERM) <
						text = <"Set Score 3">
					>
					["gt0015"] = (TERM) <
						text = <"Set Score 4">
					>
					["gt0016"] = (TERM) <
						text = <"Calculate score">
					>
					["gt0017"] = (TERM) <
						text = <"Set N/A">
					>
				>
			>
		>
	>
>
