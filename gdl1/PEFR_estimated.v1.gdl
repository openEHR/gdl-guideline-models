(GUIDE) <
	gdl_version = <"0.1">
	id = <"PEFR_estimated.v1">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"PEF", "PEFR", "asthma", "COPD", "airway obstruction">
				misuse = <"Only for use if patient's baseline PEF is not known.">
				purpose = <"To estimate the baseline expected peak expiratory flow for patients where previously unknown.">
				use = <"To record the estimated expected peak expiratory flow for different age groups and sexes. Many equations exist for estimating PEF and some examples for chidren and adults are as below:
Children:
 PEFR (L/min) = ((Height in cm - 100) * 5) + 100
Adult males:
 PEFR (L/min) =  (((Height in metres * 5.48) + 1.58) - (Age * 0.041)) * 60
Adult females:
 PEFR (L/min) =  (((Height in metres * 3.72) + 2.24) - (Age * 0.03)) * 60
This application accepts height input in cm or inches.">
			>
			["sv"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"PEF", "astma", "KOL", "luftvägsobstruktion", "peak expiratory flow">
				misuse = <"Använd ej för registrering av värden erhållna med hjälp av spirometri eller PEF-mätare.">
				purpose = <"Att med hjälp av formler uppskatta PEF-värde hos vuxna och barn.">
				use = <"Att registrera uppskattat förväntat PEF-värde bland olika åldersgrupper och kön. Det finns flera olika formler för uppskattning av PEF-värde och dessa används för denna riktlinje:

Barn:
PEF (l/min) = ((längd, cm - 100) * 5) + 100
Vuxna män:
(((längd, m * 5,48) + 1.58) - (ålder * 0.041)) * 60
Vuxna kvinnor:
(((längd, m * 3.72) + 2.24) - (ålder * 0.03)) * 60
För registering av värden erhållna med hjälp av spirometri eller PEF-mätare, vänligen använd openEHR-EHR-OBSERVATION.pulmonary_function.">
			>
		>
		lifecycle_state = <"Author draft">
		original_author = <
			["date"] = <"2017-03-21">
			["email"] = <"models@cambiocds.com">
			["name"] = <"Eneimi Allwell-Brown">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_contributors = <"Dennis Forslund", "Jimmy Axelsson">
		other_details = <
			["references"] = <"Knudson RJ, Lebowitz MD, Holberg CJ, Burrows B. Changes in the normal maximal expiratory flow-volume curve with growth and aging 1–3. American Review of Respiratory Disease. 1983 Jun;127(6):725-34.

Nunn AJ, Gregg I. New regression equations for predicting peak expiratory flow in adults. BMj. 1989 Apr 22;298(6680):1068-70.

Balasubramanian S, Ravikumar NR, Chakkarapani E, Shivbalan SO. Peak expiratory flow rate in children-A ready reckoner. Indian pediatrics. 2002 Jan 24;39(1):104-5.">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0002"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.basic_demographic.v1">
				domain = <"EHR">
				elements = <
					["gt0003"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0008]">
					>
					["gt0004"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0005"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.height.v1">
				domain = <"EHR">
				elements = <
					["gt0006"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0007"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.peak_expiratory_flow_estimated.v1">
				domain = <"CDS">
				elements = <
					["gt0008"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
				>
			>
			["gt0009"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.height.v1">
				domain = <"CDS">
				elements = <
					["gt0010"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0003]/items[at0004]">
					>
				>
			>
		>
		rules = <
			["gt0011"] = (RULE) <
				when = <"$gt0006.units=='in'",...>
				then = <"$gt0010.precision=2", "$gt0010.units='cm'", "$gt0010.magnitude=($gt0006.magnitude*2.54)">
				priority = <5>
			>
			["gt0012"] = (RULE) <
				when = <"$gt0006.units=='cm'",...>
				then = <"$gt0010=$gt0006",...>
				priority = <4>
			>
			["gt0013"] = (RULE) <
				when = <"$gt0003.value>=($currentDateTime.value-18,a)",...>
				then = <"$gt0008.units='l/min'", "$gt0008.magnitude=((($gt0010.magnitude-100)*5)+100)">
				priority = <3>
			>
			["gt0014"] = (RULE) <
				when = <"$gt0003.value<($currentDateTime.value-18,a)", "$gt0004==local::at0006|Female|">
				then = <"$gt0008.units='l/min'", "$gt0008.magnitude=((((($gt0010.magnitude/100)*3.72)+2.24)-(($currentDateTime.year-$gt0003.year)*0.03))*60)">
				priority = <2>
			>
			["gt0015"] = (RULE) <
				when = <"$gt0003.value<($currentDateTime.value-18,a)", "$gt0004==local::at0005|Male|">
				then = <"$gt0008.units='l/min'", "$gt0008.magnitude=((((($gt0010.magnitude/100)*5.48)+1.58)-(($currentDateTime.year-$gt0003.year)*0.041))*60)">
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Peak Expiratory Flow - estimated">
						description = <"Peak expiratory flow (peak expiratory flow rate, PEFR) is the maximum flow rate of air generated during forceful exhalation from fully inflated lungs. It is a measure of flow through large airways and though traditionally measured by a portable peak flow meter or by spirometry, in their absence a baseline expected value can be estimated using various formulae that generally include the individual's age, height and sex. PEFR is measured in litres per minute (L/min) and is invaluable in assessing and monitoring the degree of airway obstruction in patients with asthma and chronic obstructive pulmonary disease (COPD). ">
					>
					["gt0003"] = (TERM) <
						text = <"Birthdate">
						description = <"The patient's date of birth.">
					>
					["gt0004"] = (TERM) <
						text = <"Gender">
						description = <"The patient's gender.">
					>
					["gt0006"] = (TERM) <
						text = <"Height/Length">
						description = <"The length of the body from crown of head to sole of foot.">
					>
					["gt0008"] = (TERM) <
						text = <"Peak expiratory flow (estimated)">
						description = <"Estimated expected PEFR for age and gender.">
					>
					["gt0010"] = (TERM) <
						text = <"Height/Length">
						description = <"The length of the body from crown of head to sole of foot.">
					>
					["gt0011"] = (TERM) <
						text = <"Convert height from inches to cm">
					>
					["gt0012"] = (TERM) <
						text = <"Set height">
					>
					["gt0013"] = (TERM) <
						text = <"Calculate estimated PEFR child">
					>
					["gt0014"] = (TERM) <
						text = <"Calculate estimated PEFR adult female">
					>
					["gt0015"] = (TERM) <
						text = <"Calculate estimated PEFR adult male">
					>
				>
			>
			["sv"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Uppskattat PEF-värde">
						description = <"Peak expiratory flow (PEF – topputandningsflöde), är ett mått på maximalt forcerat utandningsflöde. PEF mäts vanligtvis med hjälp av spirometri eller PEF-mätare, men ett basvärde på förväntat resultat kan uppskattas genom olika formler baserat på ålder, längd och kön. PEF-värden anges i liter per minut (l/min) och används för att följa och utvärdera grad av luftvägsobstruktion hos patienter med astma och kronisk exspiratorisk lungsjukdom (KOL).">
					>
					["gt0003"] = (TERM) <
						text = <"Födelsedatum">
						description = <"Patientens födelsedatum,">
					>
					["gt0004"] = (TERM) <
						text = <"Kön">
						description = <"Patientens kön.">
					>
					["gt0006"] = (TERM) <
						text = <"Längd">
						description = <"Kroppslängd.">
					>
					["gt0008"] = (TERM) <
						text = <"Uppskattat PEF-värde">
						description = <"Förväntat PEF-värde baserat på ålder och kön,">
					>
					["gt0010"] = (TERM) <
						text = <"Längd">
						description = <"Kroppslängd.">
					>
					["gt0011"] = (TERM) <
						text = <"Konvertera längd från tum till cm">
					>
					["gt0012"] = (TERM) <
						text = <"CDS längd">
					>
					["gt0013"] = (TERM) <
						text = <"Beräkna uppskattat PEF-värde för barn">
					>
					["gt0014"] = (TERM) <
						text = <"Beräkna uppskattat PEF-värde för vuxna kvinnor">
					>
					["gt0015"] = (TERM) <
						text = <"Beräkna uppskattat PEF-värde för vuxna män">
					>
				>
			>
		>
	>
>
