(GUIDE) <
	gdl_version = <"0.1">
	id = <"QTc_Calculation.v1">
	concept = <"gt0001">
	language = (LANGUAGE) <
		original_language = <[ISO_639-1::en]>
	>
	description = (RESOURCE_DESCRIPTION) <
		details = <
			["en"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"electrocardiogram", "ECG", "EKG", "QT interval", "Bazett’s formula", "arrhythmia", "QT correction", "cardiology">
				purpose = <"To estimate the QTc which represents the QT interval at a heart rate of 60 bpm, allows comparison of QT interval values at different heart rates, and improves detection of patients at increased risk of arrythmia.">
				use = <"Use to calculate QTc based on heart rate (obtainable by palpation or ECG) and QT interval (obtained from the ECG). QTc is expressed in milliseconds (msecs) and is estimated using the heart rate (bpm) and QT interval (msecs) as variously expressed by the following formulae:
Bazett: QTc = QT / RR^0.5
Fredericia: QTc = QT / RR^0.333
Framingham: QTc = QT + 0.154 (1 – RR)
Hodges: QTc = QT + 1.75 (heart rate – 60)
where RR = R-R interval = 60 / heart rate.
QTc is prolonged if > 440ms in men or > 460ms in women, and associated with increased risk of torsades de pointes if > 500ms in any sex. QTc is abnormally short if < 350ms.
Bazett’s formula is simple and is the most commonly used. It provides an adequate correction for heart rates ranging from 60 – 100 bpm. It over-corrects at heart rates > 100 bpm and under-corrects at heart rates < 60 bpm.
Fredericia or Framingham corrections are more accurate at heart rates outside of the 60 – 100 bpm range.">
			>
			["sv"] = (RESOURCE_DESCRIPTION_ITEM) <
				copyright = <"© Cambio Healthcare Systems">
				keywords = <"elektrokardiogram", "EKG", "QT-intervall", "Bazett", "arytmi", "korrigerad QT-tid", "QTc", "QT-tid", "kardiologi">
				purpose = <"Att beräkna en uppskattning av korrigerad QT-tid (QTc) vilket representerar QT-tid vid en hjärtfrekvens om 60 slag per minut. QTc medger jämförelser av QT-tid över olika hjärtfrekvenser och kan användas för att upptäcka individer med förhöjd risk för arytmi.">
				use = <"Använd för att beräkna korrigerad QT-tid (QTc) baserad på hjärtfrekvens och QT-intervall. QTc uttrycks i millisekunder (ms) och kan beräknas med hjälp av följande formler:

Bazett: QTc = QT / RR^0.5
Fredericia: QTc = QT / RR^0.333
Framingham: QTc = QT + 0.154 (1 – RR)
Hodges: QTc = QT + 1.75 (hjärtfrekvens – 60)
RR = R-R-intervall = 60 / hjärtfrekvens

QTc är förlängd om >440ms (man) eller >460s (kvinna). Om QTc >500ms oavsett kön, föreligger en förhöjd risk för förekomst av torsades de pointes. QTc <350ms definieras som abnormt kort.

Bazett är den mest använda formeln för beräkning av QTc, och ger en adekvat justering för hjärtfrekvenser om 60-100 slag per minut, men över- respektive underkorrigerar för hjärtfrekvenser >100 respektive <60 slag per minut. För dessa intervall ger istället Fredericia och Framingham ökad precision.
">
			>
		>
		lifecycle_state = <"Author draft">
		original_author = <
			["date"] = <"2017-01-12">
			["email"] = <"models@cambiocds.com">
			["name"] = <"Eneimi Allwell-Brown">
			["organisation"] = <"Cambio Healthcare Systems">
		>
		other_contributors = <"Dennis Forslund", "Jimmy Axelsson">
		other_details = <
			["references"] = <"Bazett HC. AN ANALYSIS OF THE TIME‐RELATIONS OF ELECTROCARDIOGRAMS. Annals of Noninvasive Electrocardiology. 1997 Apr 1;2(2):177-94.

Fridericia LS. The duration of systole in an electrocardiogram in normal humans and in patients with heart disease. Annals of Noninvasive Electrocardiology. 2003 Oct 1;8(4):343-51.

Hodges M, Salerno D, Erlien D. BAZETT QT CORRECTION REVIEWED-EVIDENCE THAT A LINEAR QT CORRECTION FOR HEART-RATE IS BETTER. InJournal of the American College of Cardiology 1983 Jan 1 (Vol. 1, No. 2, pp. 694-694). 655 AVENUE OF THE AMERICAS, NEW YORK, NY 10010: ELSEVIER SCIENCE INC.

Sagie A, Larson MG, Goldberg RJ, Bengtson JR, Levy D. An improved method for adjusting the QT interval for heart rate (the Framingham Heart Study). The American journal of cardiology. 1992 Sep 15;70(7):797-801.">
		>
	>
	definition = (GUIDE_DEFINITION) <
		archetype_bindings = <
			["gt0003"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.ecg.v1">
				domain = <"CDS">
				elements = <
					["gt0004"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0005]/items[at0006]/items[at0008]">
					>
					["gt0014"] = (ELEMENT_BINDING) <
						path = <"/protocol[at0003]/items[at0025]">
					>
				>
			>
			["gt0005"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.ecg.v1">
				domain = <"EHR">
				elements = <
					["gt0006"] = (ELEMENT_BINDING) <
						path = <"/data[at0001]/events[at0002]/data[at0005]/items[at0006]/items[at0007]">
					>
					["gt0010"] = (ELEMENT_BINDING) <
						path = <"/protocol[at0003]/items[at0025]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
			["gt0007"] = (ARCHETYPE_BINDING) <
				archetype_id = <"openEHR-EHR-OBSERVATION.pulse.v1">
				domain = <"EHR">
				elements = <
					["gt0008"] = (ELEMENT_BINDING) <
						path = <"/data[at0002]/events[at0003]/data[at0001]/items[at0004]">
					>
				>
				predicates = <"max(/data/events/time)",...>
			>
		>
		rules = <
			["gt0002"] = (RULE) <
				when = <"$gt0010==local::at0083|Bazett’s formula|",...>
				then = <"$gt0004.units='millisec'", "$gt0004.magnitude=($gt0006.magnitude/((60/$gt0008.magnitude)^0.5))">
				priority = <4>
			>
			["gt0011"] = (RULE) <
				when = <"$gt0010!=null",...>
				then = <"$gt0014=$gt0010",...>
				priority = <5>
			>
			["gt0017"] = (RULE) <
				when = <"$gt0010==local::at0085|Framingham formula|",...>
				then = <"$gt0004.units='millisec'", "$gt0004.magnitude=($gt0006.magnitude+(0.154*(1-(60/$gt0008.magnitude))))">
				priority = <3>
			>
			["gt0018"] = (RULE) <
				when = <"$gt0010==local::at0086|Hodges formula|",...>
				then = <"$gt0004.units='millisec'", "$gt0004.magnitude=($gt0006.magnitude+(1.75*($gt0008.magnitude-60)))">
				priority = <2>
			>
			["gt0019"] = (RULE) <
				when = <"$gt0010==local::at0084|Fredericia’s formula|",...>
				then = <"$gt0004.units='millisec'", "$gt0004.magnitude=($gt0006.magnitude/((60/$gt0008.magnitude)^0.333))">
				priority = <1>
			>
		>
	>
	ontology = (GUIDE_ONTOLOGY) <
		term_definitions = <
			["en"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Corrected QT Interval (QTc) Calculator">
						description = <"Calculates the corrected QT interval (QTc), which is an estimate of the QT interval at a heart rate of 60 beats per minute (bpm). QTc allows comparison of QT values over time at different heart rates and improves detection of patients at increased risk of arrhythmias. QTc is expressed in milliseconds and is estimated using the heart rate (bpm) and QT interval (milliseconds) as variously expressed by the Bazett, Fredericia, Framingham and Hodges formulae. QTc is prolonged if > 440ms in men or > 460ms in women, and associated with increased risk of torsades de pointes if > 500ms in any sex. QTc is abnormally short if < 350ms.">
					>
					["gt0002"] = (TERM) <
						text = <"Calculate Bazett QTc">
					>
					["gt0004"] = (TERM) <
						text = <"QTc interval">
						description = <"Corrected QT interval.">
					>
					["gt0006"] = (TERM) <
						text = <"QT interval">
						description = <"Interval from the onset of the QRS complex to the end of the T wave.">
					>
					["gt0008"] = (TERM) <
						text = <"Heart Rate">
						description = <"The rate, measured in beats per minute.">
					>
					["gt0010"] = (TERM) <
						text = <"QTc calculation method">
						description = <"Method used to correct QT interval.">
					>
					["gt0011"] = (TERM) <
						text = <"Set QTc protocol">
					>
					["gt0014"] = (TERM) <
						text = <"QTc calculation method">
						description = <"Method used to correct QT interval.">
					>
					["gt0017"] = (TERM) <
						text = <"Calculate Framingham QTc">
					>
					["gt0018"] = (TERM) <
						text = <"Calculate Hodges QTc">
					>
					["gt0019"] = (TERM) <
						text = <"Calculate Fredericia QTc">
					>
				>
			>
			["sv"] = (TERM_DEFINITION) <
				terms = <
					["gt0001"] = (TERM) <
						text = <"Korrigerad QT-tid (QTc)">
						description = <"Beräknar korrigerad QT-tid (QTc), vilket är en uppskattning av QT-tid vid en hjärtfrekvens om 60 slag per minut. QTc medger jämförelser av QT-tid över olika hjärtfrekvenser och kan användas för att upptäcka individer med förhöjd risk för arytmi. QTc uttrycks i millisekunder (ms) och kan beräknas med hjälp av Bazett-, Fredericia-, Framingham- och Hodges-formlerna. QTc är förlängd om >440ms (man) eller >460s (kvinna). Om QTc >500ms oavsett kön, föreligger en förhöjd risk för förekomst av torsades de pointes. QTc <350ms definieras som abnormt kort.">
					>
					["gt0002"] = (TERM) <
						text = <"Beräkna QTc enligt Bazett ">
					>
					["gt0004"] = (TERM) <
						text = <"QTc">
						description = <"*(en) Corrected QT interval.">
					>
					["gt0006"] = (TERM) <
						text = <"QT-intervall">
						description = <"*(en) Interval from the onset of the QRS complex to the end of the T wave.">
					>
					["gt0008"] = (TERM) <
						text = <"Hjärtfrekvens">
						description = <"*(en) The rate, measured in beats per minute.">
					>
					["gt0010"] = (TERM) <
						text = <"QTc metod för beräkning">
						description = <"*(en) Method used to correct QT interval.">
					>
					["gt0011"] = (TERM) <
						text = <"CDS QTc protokoll">
					>
					["gt0014"] = (TERM) <
						text = <"QTc metod för beräkning">
						description = <"*(en) Method used to correct QT interval.">
					>
					["gt0017"] = (TERM) <
						text = <"Beräkna QTc enligt Framingham ">
					>
					["gt0018"] = (TERM) <
						text = <"Beräkna QTc enligt Hodges">
					>
					["gt0019"] = (TERM) <
						text = <"Beräkna QTc enligt Fredericia">
					>
				>
			>
		>
	>
>
